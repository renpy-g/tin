image stuBg1 = "images/backgrounds/stuBg/stu-1.png"

# 3 공부
# 무지로 따르는 것이 순종인가?
label study:
    scene stuBg1 with dissolve
    $ change_image('base_2')
    T "아, 와, 왔구나."
    T "[you], 나 좀 도와, 도와줄래? 큰일이야, 아직도 자, 잘 모르겠는데."

    menu:
        "무슨 일이야?":
            $ belief += 3
            T "조, 좀 있으면 시험을 보는데, 통과하지 못하면, 으음, 안 좋아."
            T "그런데 여전히 잘, 모르겠어..."

        "싫어.":
            $ belief -= 2
            T "그, 그러지 말고 제발... [you]의 역할이 나, 나를 가르치는 거잖아. 내가 미숙하면 [you]에게도 아, 안 좋아... 아... 마도..."

        "당연히 도와줄게.":
            $ belief += 5
            $ change_image('smail_5')
            T "정말? 고마, 고마워 [you]...! 으응, {은는}[you]{/은는} 똑똑하니까, 부, 분명 잘 알려줄, 거야."

    T "부탁해. 나, 날 가르쳐줘. {이가}[you]{/이가} 알려준 대로, 외울 테니까..."

    menu:
        "무슨 시험이야?":
            $ belief += 2
            T "교리의 내용을 잘 이해하고 있는지... 에 대한 시험이야."
            T "그분을 더 잘 알아야, 더 깊게 섬길 수 있다고..."

        "귀찮아.":
            $ belief -= 2
            $ change_image('base_6')
            T "으... 미안... {은는}[you]{/은는} 괜찮겠지만, 난 계속 재시험이 이, 있어서... 어쩔 수 없어..."

        "열심히 하자.":
            $ belief += 4
            $ change_image('smail_1')
            T "으응...! 히, 힘낼게. 조금 시끄러워도, [you]의 말 제대로... 들을 테니까."
            "이곳은 고요하다. 귀를 기울이면 서로의 숨소리도 들을 수 있을 만큼."

    $ change_image('base_2')
    T "너마저 도와, 주는데 또 떨어지면 엄청 호, 혼날 거야."
    T "일단 여, 여기 교리서랑, 시험지... 공부하면서 모르는 거 있으면, 물어볼게..."
    T "아, 음, 심심하면 같이, 풀래? 그러니까 같이... 공부하잔, 뜻이야."
    $ change_image('smail_5')
    T "그럼 뭔가 더, 보람찰지도 모르잖아."

    menu:
        "너나 해.":
            $ belief -= 1
            $ change_image('eye_right')
            T "그으... 그래, 알았어..."
            T "미리 공부하면, 나중에 좋을... 텐데."

        "그래.":
            $ belief += 4
            T "하하, 그, 그분이 기뻐하시겠네. 누가 자신, 에 대해 알아가면, 시, 신이라도 기쁜가 봐."
            T "너도 그래 [you]?"
            # 그냥 깜빡이는 연출도 괜찮을 듯? 근데 어케하지
            $ change_image('smail_10')
            T "난 무척 공포스러워."

    $ change_image('panic_6')
    "책상 위로 교리서와 시험지가 나뒹굴었다. 눈앞의 [lamb]는 끙끙대며 머리를 싸매고 있지만... 문제를 풀긴 하나보다."

    menu:
        '슬쩍...{fast}'
        "[lamb]의 시험지를 본다.":
            show expression AlphaMask("black70", At(new, center)) as mask at multiply with Dissolve(0.2)
            "시험지 위에 난잡하게 그려진 글자는 특이하게도 악필과 명필이 뒤섞여 있었다. 같은 글자임에도 불규칙적이게 글씨가 달라진다."
            "상단에는 이름이 적혀있다. {i}'틴데라이 아이비 헨리에트'.{/i}\n연습 시험지에도 이름을 쓰던가?"
            hide mask with Dissolve(0.2)
            $ change_image('base_2')
            T "왜... 왜 그래?"
            "너의 시선을 눈치챈 [lamb]가 머쓱하게 눈치 보며 물었다."

            menu:
                "이름을 봤어.":
                    if lamb == '아그넬로':
                        if you == '엘시':
                            $ belief += 6
                            $ change_image('smail_1')
                            T "아... 그게, 아그넬로는 우리끼리의 이, 이름이잖아? 여기 적는다고 해도, 못 알아보고... 그러니까 어, 다들 알아보는... 이름을 쓴 거야."
                            T "... 나는 엘시만이, 아그넬로라 불러주는 게 제일, 좋아."
                            T "... ... {nw}"
                            $ change_image('eye_bottom')
                            extend "아니야... ..."
                            "'틴데라이'의 뒤에 붙은 문자에 난잡한 선들이 거칠게 그어졌다."
                            "곧 새로운 이름을 다시 적는다."
                            "{i}'틴데라이 아그넬로'{/i}"
                            $ change_image('smail_5')
                            T "하하... 이게 나의 온전한 기원이지. 그, 그치?"
                            T "용서해 줘 엘시. 집안이라는 거, 이제 나와 상관 없는데... 다, 당연한 사실을, 잊고 있었어. 미안해."
                            $ change_image('smail_10')
                            "[lamb]는 웃었다."
                        else:
                            $ change_image('base_1')
                            T "너에게 아, 아그넬로를 보이고 싶지 않아. 이건 엘시만이 부를 수 있는, 이름이야."
                    else:
                        $ belief += 2
                        $ change_image('smail_1')
                        T "어... 원래는 헨리에트만 있으면 되, 되는데... 이번에는 그냥, 아이비도 넣어봤어."
                        T "여기선 이름만 있어도 추, 충분하지만. 가끔은 이런 것도... ... 안 괜찮나."
                        T "아이비는 어머니의 이름, 이야. 아, 아버지 이름은 바스티온이고... 하하. 이런 거 별로, 안 궁금하겠지."
                        $ change_image('eye_bottom')
                        T "가끔 어머니가 그리워... ..."

                "글씨가 지저분해.":
                    hide mask
                    $ belief -= 1
                    $ change_image('smail_5')
                    T "으응 미, 미안... 나도 가끔 알아보기가, 어려워. 하하."
                    T "시야가 종종 뒤, 틀려서. 가리는 것도 많고... 음, 눈은 나쁘지 아, 않은데. 왤까?"
                    T "글씨를 잘 쓰려고 아주 많이, 연습했었는데..."
                    $ change_image('smail_1')
                    T "그분 외엔 아무것도 의미가 없나 봐."

                "많이 어려워?":
                    hide mask
                    $ belief += 2
                    T "아, 아냐. 할 수 있어... 어렵, 어렵긴 한데... 괜찮아."
                    T "어릴 때는 머, 머리가 좋았는데... 왜 지금은... 내, 내가 그렇지 뭐... ..."

                    menu:
                        "지금은 귀여워.":
                            $ belief += 1
                            $ change_image('say_2')
                            T "어... 멍, 청해져서, 귀엽다는 뜻이야?"
                            T "그치만 귀, 귀여운 게 무슨... 쓸모가 있겠어."

                        "지금도 똑똑해.":
                            $ belief += 2
                            $ change_image('smail_10')
                            T "... 헤헤. 그렇게 말, 해줘서, 고마워."
                            T "그래도 나보단 {이가}[you]{/이가} 더, 더 똑똑하지. 그래서 선생님을 하, 하는 거잖아."

                        "그러게.":
                            $ belief -= 2
                            $ change_image('eye_bottom')
                            T "... 다시는 돌아오지 못할, 테니까. 슬퍼."

        "교리서를 본다.":
            $ belief += 3
            show expression AlphaMask("black70", At(new, center)) as mask at multiply
            "손가락 하나 굵기의 교리서는 펼쳐보면 의외로 작은 글씨들이 빽빽하게 쓰여있다."
            "가독성이 좋지 않고 알아보기 힘든 글자들이 많다."
            "자세히 살펴보면 아주 많은 규칙들의 나열이란 것을 알 수 있다. 가끔 비슷한 말들이 섞여 다른 규칙인 것 마냥 존재하는 글도 있는 듯하다."
            "[lamb]에게 교리 중 하나를 물어볼까?"

            menu:
                "연애금지.":
                    $ belief += 3
                    "{은는}[you]{/은는} '연애금지' 교리에 대해 물었다."
                    hide mask
                    $ change_image('base_2')
                    T "아... 그건 말이지. 어, 단순, 단순하게... 다른 것을 사랑하면, 그분을 향한 사랑이 줄어, 드니까 그런 거랬어."
                    T "그래도 몰래 하는, 사람도 있겠지? 누, 눈에 보이는 존재가... 더 와닿고 사랑하기, 쉽잖아."
                    $ change_image('eye_right')
                    T "곁에 있으면 더... 그렇고."
                    T "사, 사랑은 잘 모르지만, 인간이 사랑에 빠질 만한 논리 정도는, 조금 알고 있어."

                    if love:
                        $ belief += 4
                        $ change_image('smail_5')
                        T "네가 내 신, 이어서 정말 다, 다행이야."
                        $ change_image('smail_8')
                        T "너의 존재를 명확히 느낄 수 있어서 기뻐... ..."
                        T "이것도 분명, 축복이지? 그렇지?"
                        T "널 만나서, 무척 행복해..."

                "소리내어 웃지말라.":
                    $ belief += 3
                    "{은는}[you]{/은는} '소리내어 웃지말라.'는 교리에 대해 물었다."
                    hide mask
                    $ change_image('eye_right')
                    T "정확히는 큰 소리로 우, 웃지 말라는 건데... 으음, 그건 나도 자, 잘 모르겠어."
                    T "크게 웃는 건 행, 복해서 웃는 거잖아? 그런데 그 행복에, 그분은 속하지 않았으니까... 무례한 행동, 이래."
                    T "그분은 우리가 고통 속에서 감사해야, 기뻐한다고... 그러니까 행복을 티, 티내지 않는 게, 예의라면서..."
                    $ change_image('say_2')
                    T "사실 이해가 아, 안돼. 행복하면 좋은, 거 아냐?"
                    T "행복의 종류를 구, 굳이 따져야, 해?"
                    $ change_image('base_2')
                    T "... 내가, 궁금해 한다고... 딱히 달라지는 건, 없지만."

                "몸을 정갈하게.":
                    $ belief += 3
                    "{은는}[you]{/은는} '몸을 정갈하게.'라는 교리에 대해 물었다."
                    hide mask
                    $ change_image('base_2')
                    T "그건 어, 어쩌면 우리 종교의, 가장 근본일지도 몰라. 아, 음, 말이 이상하지..."
                    T "정갈, 하다는 건... 그냥 몸이 건강하고, 상처가 없는 걸 마, 말해. 그분은 그걸 좋아, 하신대."
                    T "난 항상 허약하고 종종 다, 다치니까. 그분에게 축복을 받지, 못한다 그랬어."
                    T "항상 부족해... ..."
                    T "그보다 정신의 정결함은 어, 없어도 되는 걸까?"
                    T "... 내가, 궁금해 한다고... 딱히 달라지는 건, 없지만."

                "관심없다.":
                    $ belief -= 2
                    "대충 눈으로 훑은 교리 중엔 의외로 정상적인 문구도 있었다."
                    "'감사히 밥을 먹어라', '늘 청결을 유지해라', '다치지 않게 주의해라' 등등..."
                    "은근히 신도들의 건강을 신경쓰는 듯하다."


        "관둔다.":
            show expression AlphaMask("black70", At(new, center)) as mask at multiply
            "{은는}[you]{/은는} 이 모든 것에서 눈을 뗐다. 오늘 [you]의 일은 이 공간에서 [lamb]의 공부가 끝날 때까지 함께하는 것뿐이니... 지루한 시간이 흐른다."

    window hide # 대사창을 닫음!!
    # 마우스 좌클릭 불허 설정
    $ use_click(False)

    # 그냥 다 하이드 시켜

    # 2초를 기다림!!
    pause(2)    

    $ change_image('panic_6')
    show expression AlphaMask("black70", At(new, center)) as mask at multiply

    # 0.1초뒤에 마우스 좌클릭 허용 설정
    $ threading.Timer(0.1, use_click, [True]).start()

    "그 후 어느 정도 시간이 지났을 때쯤 곤란한 얼굴의 [lamb]가 [you]에게 물었다."
    $ change_image('say_2')
    T "있지 [you], 전부터 느, 늘 궁금했는데 교리서엔 쓰여있지, 않아서... 하나 물어봐도, 돼?"

    menu:
        "그래.":
            $ belief += 3
            $ change_image('smail_5')
            T "고, 고마워. {은는}[you]{/은는} 나보다 잘 아, 알 테니까. 분명 알려줄 거야."

        "못 찾은 거 아냐?":
            T "아, 아냐. 몇 번이고 정독, 했는데 정말로 아, 안 보였어. 어제도 열 번이나 읽었는데..."

        "안돼.":
            $ belief -= 2
            T "그러, 그러지 말고, 응? 물어보는 거라도 드, 들어줘. 부탁이야."

    $ change_image('base_2')
    T "그게 뭐, 뭐냐면... 원래 종교는 창시자, 라는 게 있잖아? 그리고 그 창시자의 뒤를 잇는... 그러니까, 리더가, 있잖아. 종교 용어로는 교주라고, 부르지만."
    T "우리의 종교는 왜, 왜 교주가 보이지, 않을까? 여기 지내면서 단 하, 한 번도... 본 적이 없어."
    T "대부분의 종교는 교주를 소개, 하잖아. 대단하고 중요, 한 사람이니까. 그런데 우리는, 왜?"
    T "이, 이유를 알 것 같아 [you]?"

    menu:
        "정말로 존재하지 않아서.":
            $ belief -= 3
            T "아 그, 그런 거야...? 정말 존재하지 않으니까, 안 보인 거라고...?"
            T "그럼 이곳의 운영은 어떻게... ... 트, 특별히 높아 보이는 사람은 어, 없었어. 누군가가 지시를 내려야, 집단이 움직이잖아."
            T "여기 모든 어른들은 동등해 보였는데..."

        "몰라.":
            $ belief -= 2
            T "[you]도 몰라? 그, 그럼 선생님한테 직접 물, 어봐야 되는데... 으으 관둘래."
            T "선생님의 답은 항상 빙빙, 돌려 말해서 어려워."

        "신의 뜻이야.":
            $ belief += 4
            T "아... 그렇구나... 그, 치. 신의 뜻인데 내, 내가 함부로 궁금해하면, 안 되는 거겠지..."
            T "순종해야, 참된 신도니까. 이해하지 못해도 따라야 하니까..."
            T "알려줘서 고, 고마워. [you]에게 가르침을 받아서 기, 기뻐."

    T "또 얘기하고 싶었던, 게 있는데. 이건 그, 그냥 얘기할 사람이 없, 어서... {이랑}[you]{/이랑} 말해보... 고 싶어.
    "

    window hide # 대사창을 닫음!!
    # 마우스 좌클릭 불허 설정
    $ use_click(False)

    # 무슨 표정을 넣을까요~~
    $ change_image('eye_bottom_3')

    # 2초를 기다림!!
    pause(2)    
    # 0.1초뒤에 마우스 좌클릭 허용 설정
    $ threading.Timer(0.1, use_click, [True]).start()

    
    $ change_image('base_2')
    T "왜, 왜 생명의 탄생은 고귀하다면서, 탄생을 위한 행위는 불결하다고 하, 하는 거지?"
    T "... 이런 얘기가 싫, 다면 그만할게..."

    menu:
        "괜찮아.":
            $ belief += 3
            $ change_image('say_2')
            T "괜찮아? 그래..."
            T "내가 하, 하고 싶었던 말은... 모순적이란 거야. 모순, 이라는 건 좋은 의미는 아니잖아?"
            T "나도 모순을 싫어해. 인지하면 내장이 비틀리는, 감각이어서..."
            T "그런데. 모두 모순을 갖고 있어. 모순은 우리의 일부야."
            T "왜냐하면, 모순되지 않는 존재는... ..."
            $ change_image('eye_shut')
            T "...... ...... ......"

            window hide # 대사창을 닫음!!
            # 마우스 좌클릭 불허 설정
            $ use_click(False)
            # 2초를 기다림!!
            pause(2)    
            # 0.1초뒤에 마우스 좌클릭 허용 설정
            $ threading.Timer(0.1, use_click, [True]).start()

            menu:
                "... [lamb]?":
                    $ belief += 3

                "괜찮아?":
                    $ belief += 0

                "왜 그래?":
                    $ belief += 0

            $ change_image('mung')
            "깜빡."
            T "... 아. 미, 미안. 잠깐 정신을 잃었나, 봐. 종종 그러니까 괘, 괜찮아."
            T "이야, 기를 이어가자면... 어, 왜 탄생을 위한 행위를, 불결하다 여기는지는 이, 이해가 돼."
            T "쾌락은 악의 그, 근본이잖아? 바탕이라고 해야 하나? 말이 이상, 이상한데 알아들었을, 거라고 믿을게."
            T "아무튼 그런 것에 빠져살면, 그분도 싫어하시겠지. 자신에게서 눈을, 돌리게 되니까."
            $ change_image('base_2')
            T "... 내가 무슨 말을 하고 싶었던 거지?"


        "싫어.":
            $ change_image('eye_right')            
            T "아... 시, 싫구나. 그럼 다른, 이야기를... 자, 잠깐만."
            T "음, 음, 어, 이런 건 어때? 종교에 관련된, 신화를 보면... 신의 이름으로 다, 다양한 걸 하잖아?"
            T "좋은 일도 하고, 나쁜 일도... 하고."
            T "전쟁을 일으킨다거나, 학살을 하기도 하는데... 그, 그래도 되는 걸까?"
            T "신의 뜻이라기엔 대, 대부분의 종교는 선하게 살라고, 하잖아. 그런데도 신화들은 마, 마치 숭고한 일처럼, 묘사하고... 뭔가, 맞지 않아서..."
            $ change_image('base_2')            
            T "신의 뜻에 인간의 도덕성은 필요 없어?"

            menu:
                "어려워.":
                    T "어, 어려운 이야기이긴, 하지. 나도 계속 고민 해, 했는데 답을 모르겠어."

                "인간의 거짓말이야.":
                    $ belief -= 3
                    T "인간이 시, 신의 존재를 이용했단, 뜻이야? 그건... 그건 모독이잖아. 용서받지 못할 거야..."
                    T "그럼 어떻게 거짓, 거짓말을 구분해? 신의 증거라곤 교리서, 뿐인데 이걸 해독해 주는 것도 이, 인간이잖아."
                    T "스스로 깨우치기엔 너, 너무 어렵고..."

                "필요 없어.":
                    $ belief += 4
                    T "정말로 필요 없는... 거구나. 그, 그렇구나. 신의 뜻인데, 우리가 감히 이해를 바라선... 안 되겠지..."
                    T "이해할 수 없어도... 따르는 게 그분이 원, 하시는 순종이겠지?"

                    if love:
                        $ belief += 3
                        T "나도 온전히 널 섬길게 엘시... ..."

        "공부나 해.":
            $ belief -= 2
            $ change_image('base_5')
            T "으... 물론 고, 공부도 해야 하지만... 그건 혼자서도 할 수 있, 으니까..."
            T "너와 이야기하고 싶었어."

    $ change_image('say_2')
    T "... 겨, 결국 내가 말하고 싶었던 건, 이거야."
    T "모두 다 모순을 갖고 있어. 우리는 그 모순을 풀어, 나가며 살아야 하고... 그게 삶이라고, 책에서 봤어."
    T "이 교, 교리서에 나온 내용은 아니, 야. 다른 책. 아버지 몰래 어릴 때 읽었어."
    T "인간은 완벽을 추구하며 살아야 한대. 결코 완벽할 수 없어도."
    T "자꾸 마, 말이 길어지네. 결론은 어, 별거 없는데..."
    T "있지 [you]. 우리는 모순을 받아들여야 할까? 아무것도 모른 채로... 그분만 바라, 보면서."
    T "다른 것엔 눈길도 두지 말고 맹목적으로 따르는 게, 정말, 정말로 맞는 걸까?"
    T "그분은 그걸 원하, 시는 걸까? 순종이란 말로 포장하면서?"
    $ change_image('base_1')
    T "... 아. 이, 이건 모독이지. 잘못했어. 그래도 괜찮다면 들려줘. {은는}[you]{/은는} 어떻게, 생각해?"

    menu:
        "끊임없이 스스로 생각해.":
            $ belief -= 5
            $ change_image('base_2')
            T "... 새, 생각을 하면, 이해할 수 없는 점이, 너무 많아. 이 종교는 뭔가, 이상해. 그분이 이런 생각을 시, 싫어하면, 어떡하지?"
            T "그래도 이렇, 게 고민하는 게, 올바른 진리를 찾을 수 있는 행동인 거지? 그, 그렇다고 말해줘. [you]."
            if love:
                $ belief += 10
                T "... 괜찮아. 엘시는 늘 옳았어. 어, 언제나 답을 알려주고, 날 올바른 길로 이끌어주니까..."
                T "너는 결코 의심하지 않아..."
                T "... ...{w}너도, 날 믿어줄 거지?"

            T "나는 널 믿으니까..."

        "의심을 버리고 받아들여.":
            $ belief += 4
            $ change_image('base_2')
            T "... 맞아. 그, 그래야지. 어쩌면 난 계속 시, 시험에 걸린 걸지도 몰라."
            T "그분이 널 만나게 해서, 구원을 주신 거야."
            T "가, 감사를 해야겠다. 이 기적이 끝나지 않도록 기도, 기도할래."
            T "{이가}[you]{/이가} 계속 날 깨우치길..."

        "나도 몰라.":
            $ change_image('warry')
            T "모르는 거야? 그치... 어려, 어려운 이야기였으니까... [you]도 모르는 게 이, 있구나."
            T "답을 알려주지, 않을까 기대했어."
            $ change_image('what')
            T "아, 그, 그렇다고 실망, 실망했다는 말은 아, 아니야...!"
            T "나는 계속 너를 좋아해..."

    T "음, 말이 너무 마, 많았네. 으응, 공부하기 싫어서, 그런 건 절대로... 아니니까..."
    T "이제 제대로 고, 공부할게..."

    # 확인이 필요함

    window hide # 대사창을 닫음!!
    # 마우스 좌클릭 불허 설정
    $ use_click(False)

    # 얼굴을 좀 바꿀 필요가 있음

    #사각사각... 북북... 암튼 정신사나운 소리 5초정도
    play sound "audio/sound/슥슥부욱.mp3"
    
    # 5초를 기다림!!
    pause(5)    
    
    
    # 0.1초뒤에 마우스 좌클릭 허용 설정
    $ threading.Timer(0.1, use_click, [True]).start()

    menu:
        "시끄러.":
            $ belief -= 2
            $ change_image('panic_1')
            T "아, 으, 미, 미안. 소리를 드, 듣고 싶지 않아, 서."

        "뭐 해?":
            $ belief += 1
            $ change_image('warry')
            T "어... 어? 아 조, 종이가 엉망이, 됐네..."

    T "아무, 아무것도 아냐. 미안해. 그냥 주변이 시, 끄러워서... {은는}[you]{/은는} 이 소리 아, 안 들려?"
    "공간은 지나치게 고요했다. 조금만 귀를 기울이면 불안하게 뛰는 [lamb]의 심장소리도 들을 수 있을 거라 착각할 만큼."

    menu:
        "안 들려.":
            $ change_image('panic_6')
            T "그... 그렇구나. 으응, 역시 그렇지... 내 귀가 이상, 이상해서 그래. 신경쓰이게 해서, 미안해."

        "무슨 소리?":
            $ change_image('say_2')
            T "아, 안 들리는 거야? 바깥에서 누가 자꾸, 쿵쾅거리면서 거, 걷고 있어."
            T "귀신, 귀신 같은 건 아냐. 여긴 시, 신성한 곳인데, 그런 게 왜 있, 겠어?"

        "들려.":
            $ belief += 3
            $ change_image('smail_5')
            T "정말? [you]도 드, 들리는구나. 그치? 무척 시끄, 럽잖아."
            T "누가 계속 쿵쾅거리고, 내 이름을 부르면서. 큰 소리로 고함을, 지르고 욕을 하잖아... 수학 공식을 외, 운다던가, 죽여버릴 거라고 원망하면서... ..."
            T "[you]도 들렸다니 다행, 이다. 내 차, 착각이 아니었어."

    T "종종 있는 일은 아, 아니야. 가끔씩 들리는 것뿐이니까..."
    $ change_image('base_2')
    T "늘 있는 건 {i}형제{/i}뿐인걸."
    T "난... 괘, 괜찮아. ... 걱정한 게 아니더라도 멋, 대로 생각할래."

    menu:
        "공부가 돼?":
            $ belief -= 1
            T "어... 그, 그렇게 잘 되지는, 않지만... 어쩔, 수 없어. 이번에도 토, 통과 못하면 엄청 무서운 벌, 벌을 받는다고, 했단 말야."
            T "나도 잘은 몰, 몰라. 그치만 엄청 아프고 무섭고 스, 슬플 거야..."
            $ change_image('eye_bottom')
            T "차라리 내가, 아무것도 느끼지 않았다면 좋았을 텐데."
            T "이럴 바엔 차라리 지금...... ......"

    $ change_image('what')
    # 움찔하기이
    T "...... 헉. 아, 아니야. 아니, 아무것도 아냐...!"
    T "공부... 마저 해야지... 으응, 다른 시험지를..."
    
    #부스럭~~
    play sound "audio/sound/부스럭.mp3"

    $ change_image('base_2')
    T "하아...... 어, 어릴 때라면, 이런 건 금방... 헸을 텐데."
    $ change_image('smail_1')
    T "하하. 예전에는 지금보다 더, 멀끔했어. 거, 건강하고, 영리하고."

    menu:
        "상상이 안 가.":
            $ belief -= 2
            T "그렇... 지? 지금은 허약하고, 멍청하기까지 하면서... 할 줄 아는 건 아무것도 없고......"

        "지금도 좋아.":
            $ belief += 3
            T "그, 그래? 나는 잘, 모르겠는데..."
            T "그래도... 좋아해줘서, 기뻐. 거짓말이어도."

        "어릴 때가 더 낫네.":
            $ belief -= 3
            $ change_image('base_2')
            T "그러게... 이제 모, 못 돌아가. 다신 좋아지지 못할 거야... ..."

    T "원래는 후, 후계자여서, 집안의 가업을 이을 예, 정이었어. 다들 기대를 많이 하고, 나도 그렇게 될 거라, 믿었는데..."
    $ change_image('base_2')
    T "갑자, 갑자기 마, 망가졌어. 모르겠어. 두렵다고 아, 했는데. 아무도 도, 도와주지 않, 아서..."
    $ change_image('scary_3')
    T "누구도, 내 고통을 알아주지 않아서... 모, 모두가 나를 버려, 버렸어. 어머니마저도 나를, 포기했다고...!"

    menu:
        "진정해.":
            $ belief += 2

        "괜찮아.":
            $ belief += 3

        "조용히 해.":
            $ belief -= 2

    # 이게맞나
    $ change_image('sad_2')
    T "윽 미, 미안... 이렇게 흥분, 하는 것도 조, 좋지 않은데... 고치질 못하겠어..."
    T "이야기를 이어, 가자면. 우리 집은 내가 어, 없어도, 괜찮아."
    T "아버지는 아직 젊으시고... 어머니는 힘드시니까, 첩이라도 들여서 다른 후, 계자를 만들겠지."
    T "맞아. 동생이 생길 거야. 내 진정한 {i}형제{/i}가."
    $ change_image('smail_8')
    T "아, 하하. 하, 하아... ... 내게 결혼이나 아이를 바라지 않, 으셨음 하는데. 힘드려나."
    T "{은는}[you]{/은는} 나중에... 어른, 이 되어서. 결혼할 거야? 이, 이런 질문은 실례, 인가?"

    menu:
        "안 할 거야.":
            $ change_image('base_3')
            T "안 해? 왜? 요즘은 꼭 정략이 아, 아니더라도 연애결혼도 마, 많잖아."
            T "굳이 결혼을 꼭 할, 필요는 없지만."
            $ change_image('smail_1')
            T "너도 나와 같은 거라면... 무척 반, 반갑겠다."

        "할 거야.":
            $ change_image('smail_1')
            T "역시 그으... 렇지? 보통 다 그러니까... 반려를 만나서 결혼을 하고 아, 아이를 낳고...\n{color=#2E2E2E}... 역겨워...{/color}"
            T "사, 상대는 정했어? 부모님이 짝, 지어줬다던가... 좋아하는 사, 사람이라던가."
            T "헤헤. 이러니까 음... 미, 민망한, 가십거리 같다."

            menu:
                "너랑":
                    $ belief -= 5
                    $ change_image('xx')
                    T "뭐? 어... 대체 왜...?"
                    T "아, 아니, 그, 네가 싫다는, 게 아니라..."
                    T "정말로... 이해가 안 가서..."
                    T "미안... 나 겨, 결혼 생각은 전혀 없어... 애초에 내가 그렇, 그렇게 좋은 상대도, 아니고."
                    $ change_image('xx_2')
                    T "하하. 나보다 더, 좋은 상대가 있, 을 거야. {은는}[you]{/은는} 좋은 사람이고... 분명, 사랑받을 테니까."
                    $ change_image('base_2')
                    T "그러니까... 나 말고 다른, 사람을 찾아."

                "약혼 상대랑":
                    $ change_image('base_2')
                    T "약혼자가 있구나. 그, 그럼 어쩔 수 없지..."
                    T "나도, 망가지지 않았더라면 아마 이, 있었을 텐데. 말이 나오기 전에 이렇게, 되어 버려서 다행이야... 다행, 인가...?"
                    $ change_image('smail_10')
                    T "[you]의 상대가 좋은, 사람이면 좋겠다."

                "연애 상대랑":
                    $ change_image('base_3')
                    T "와, 사귀는, 사람이라도 있어?"
                    T "아, 아니면 희망... 사항인가..."
                    $ change_image('smail_1')
                    T "잘은 모르겠지만, {이가}[you]{/이가} 행복하면 좋겠다."

                "안 알려줘.":
                    $ belief -= 2
                    T "아... 그, 그래. 예민한 주제지... 알았, 알았어. 미안."
                    T "어, 언젠가. [you]의 결혼식에, 참석하게 될까?"

        "그분과 함께할래.":
            $ belief += 6
            T "오, 아, 그렇구나. 그렇, 그렇지. 신과 함께하기 위해 도, 독신을 유지하는 사람도, 많으니까..."
            $ change_image('hope')
            T "정말 머, 멋지다 [you]..."
            T "아, 나도 그렇게 할래. 그, 그럼 우리 부모님도, 뭐라하지 않으시겠지."
            $ change_image('smail_5')
            T "네가 그분과 답을 알려줬어..."

        "이미 했어.":
            $ change_image('base_3')
            T "어? 이미 하, 한... 아, 몰랐, 몰랐어. 미안해... 혹시 자식도 있, 는 거야? 아아, 이 질문은 아니, 아니구나. 미안, 잊어줘, 미안해."

        "안 알려줘.":
            $ change_image('smail_1')
            T "으응 그, 그래. 민감한 이야, 기지. 이런 건... 그, 그럼 마음 내킬 때, 괜찮다면 들려줘. 아, 안 들려줘도 되구. 히히..."

    T "......"
    T "또 딴짓을 해버, 해버렸네. 아직도 공부할 게 많은데..."
    $ change_image('eye_bottom')
    T "음... ..."
    T "지, 지금 별로, 상태가 좋지 아, 않아. 공부는 그만, 할래. 어차피 들어오는 것도, 없으니까..."
    T "혼날지도 모르지만 괘, 괜찮아. 딱히 칭찬 받은 적도, 없어."
    $ change_image('base_2')
    T "[you], 심심하진 않아?"

    menu:
        "심심해.":
            $ change_image('smail_10')
            T "히히. 그치... 여기선 놀 것도 별로, 없는데. 으음 어딜 가든 똑같겠지만..."
            "[lamb]와 같이 주변을 둘러본다. 이 방에는 푹신한 소파, 종이와 펜, 축음기, 그밖에 자잘한 액자들이 있다."
            T "음... 지루하니까, 음악이라도 틀을까? 어차피 지금은 사, 사람도 안 지나가고..."
            T "세속적인 유행가는 여기에 없, 지만... 평범한 클래식이라면, 있어."
            $ change_image('smail_1')
            T "클래식도 괜, 어, 좋아해?"

            menu:
                "좋아해.":
                    T "그래 그럼 아, 아무거나 집어서 틀어볼게..."
                    
                    # 달칵?

                    # 대충 노래를 틈
                    play music "audio/bgm/happy-good-morning.mp3" loop fadein 0.1 fadeout 0.2 volume 0.75

                    $ change_image('base_1', Dissolve(0.4))
                    #노래를 묘사함
                    "무난한 피아노 곡이 방 안을 채운다. 느리고, 희망차다."
                    "지나치게 조용했던 이 공간에 그나마 활기를 불어넣는 것 같다."
                    $ change_image('eye_shut')
                    T "시끄럽지 않아서 좋아..."

                "싫어.":
                    T "어, 뭐라고? 자, 잘 안 들렸어. 누가 자꾸 시끄럽게, 소리 질러서..."
                    $ change_image('smail_5')
                    T "이거라도 틀면 조금은, 괜찮겠지? 적어도 머리가 울리, 울리진 않으면 좋을 텐데. 헤헤."

                    # 대충 노래를 틈
                    play music "audio/bgm/happy-good-morning.mp3" loop fadein 0.1 fadeout 0.2 volume 0.75

                    #노래를 묘사함
                    "무난한 피아노 곡이 방 안을 채운다. 느리고, 희망차다."
                    "지나치게 조용했던 이 공간에 그나마 활기를 불어넣는 것 같다."

                "찬송가를 듣자.":
                    $ belief += 3
                    $ change_image('smail_5')
                    T "그, 그거 좋은 생각이다."
                    T "어... 그런데 우리 종교에, 고유한 곡이 이, 있었던가...?"
                    "레코드 판들을 뒤적이던 [lamb]가 이내 고개를 기울이며 하나를 집어올렸다."
                    T "이건가? 일단 트, 틀어볼게."

                    stop music

                    # 달칵지이잉
                    play sound "audio/sound/달칵_지이잉.mp3"

                    ## 확인필요

                    window hide # 대사창을 닫음!!
                    # 마우스 좌클릭 불허 설정
                    $ use_click(False)

                    # 와장창ㅋ쿵탕암튼뭔가이상한소리
                    play sound "audio/sound/개시끄러움.wav" volume 0.75

                    # 탁
                    play sound "audio/sound/달칵.mp3"
                    # 2초를 기다림!!
                    pause(2)    
                    # 0.1초뒤에 마우스 좌클릭 허용 설정
                    $ threading.Timer(0.1, use_click, [True]).start()

                    $ change_image('panic_5')
                    "... ..."
                    T "... 그, 이, 이게 아닌가 봐. 잘못 골랐네. 이런, 이런 게 왜 여기 있지. 미안 그, 그냥 평범한 거 틀자."
                    # 달칵
                    play music "audio/bgm/happy-good-morning.mp3" loop fadein 0.1 fadeout 0.2 volume 0.75
                    "아까보단 훨씬 평온한 곡이 들려온다... ..."
                    $ change_image('smail_5')
                    T "하아... 바, 방금은 정말 시끄러웠어..."

            $ change_image('smail_1')
            "......{w} ......{w} ...... ......"
            T "으, 음... 여전히 조금 지루, 한가..."
            T "있지. 이거랑 비슷한 곡은, 어릴 때도 자주 드, 들었어. 우리 집안의 사람이라면, 교양이 있어야 한다면서... 아버지가 춤을 배우게, 했거든."
            T "짝을 지어서 추는, 춤이야. 호, 혼자서 추는 건 부끄럽잖아. 일반, 일반적인 무도회의 춤이었어. 왈츠지."
            T "어릴 때는 어머니랑 같이 춤을 췄어. 아직 작아서 까치발을 들었어야 했는데. 춤이 싫진 않아. 즐거웠으니까..."
            T "{은는}[you]{/은는} 춤 출 줄, 알아?"

            menu:
                "알아.":
                    $ belief += 2
                    T "아, 아는구나. 그렇지. 역시 {은는}[you]{/은는} 대단해..."
                    $ change_image('smail_5')
                    T "어... [you]만, 괜찮다면 말이야. 춤이라도 추, 출래?"
                    T "[you]라면 분명 잘 할 테니까, 같이 추, 추고 싶어."

                "몰라.":
                    $ belief += 1
                    T "몰라? 괘, 괜찮아. 요즘은 이런 춤, 구식이라고 생각, 하잖아. 시대는 바뀌니까..."
                    $ change_image('smail_5')
                    T "어... [you]만, 괜찮다면 말이야. 춤이라도 추, 출래?"
                    T "몰라도 괜찮아. 내, 내가 알려줄게."

            menu:
                "싫어.":
                    $ belief -= 2
                    $ change_image('base_2')
                    T "... 아... 그, 그래... 그럼 어쩔, 수 없지..."
                    T "음, 아, 알았어. 가만히 있을, 게."
                    $ change_image('eye_shut')
                    "[lamb]는 몸을 웅크리고 귀를 틀어막았다. 무엇도 자신을 건드리지 않는데, 간헐적으로 어깨를 튕기며 놀랐다."
                    "참으로 지루하고 평화로운 시간이 지나간다... ..."
                    "...... ......"

                "좋아.":
                    $ belief += 3
                    $ change_image('smail_6')
                    T "저, 정말? 으응, 기뻐. 어, 어울려줘서 고마워. 어쩌, 어쩌지. 무척 오랜만이어서, 긴장되는데..."
                    T "아, 아냐. 열심히 할게. 무서운 게 보여도, 눈을 감을게."
                    $ change_image('smail_2')
                    T "{을를}[you]{/을를} 믿어..."

                    call dance from _call_dance

                "제대로 신청해.":
                    $ change_image('warry')
                    T "제, 제대로...??"
                    T "어, 음, 그러면, 어어... 이, 이런 건 해본 적이, 없, 는데..."
                    "고뇌하던 [lamb]가 한 쪽 무릎을 꿇고 앉아 [you]의 손을 부드럽게 잡았다."
                    $ change_image('smail_1')
                    T "레이디... 라고 해야 하나? 레, 레이디가 아닐 수도, 있지만..."
                    $ change_image('smail_10')
                    T "하하, 엄청 바보 같은, 고민이다. 그치."
                    "곧 가벼이 웃으며 목을 가다듬고 [you]의 손등 위로 입술이 내려앉았다."
                    $ change_image('smail_5')
                    T "... [you], 저랑 한 곡 추시겠습니까?"

                    menu:
                        "수락할게.":
                            $ belief += 4
                            $ change_image('red_1')
                            T "지금 어, 엄청, 부끄러워. 기, 기쁜데 머리가 무척, 뜨거워지네."
                            T "그래도 받아줘서, 고마워... 히, 힘낼게."

                            call dance from _call_dance_1

                        "싫어.":
                            $ belief -= 4
                            $ change_image('what')
                            T "어... 어어...?! 이러, 이렇게까지 했는데도 시, 싫은 거야...?"
                            $ change_image('red_2')
                            T "나 엄청, 부끄러웠는데에... ..."
                            T "으으 귀가 무지, 뜨거워... ..."
                            $ change_image('red_3')
                            T "... 알았어. 얌전히, 있을게..."
                            "열을 식히려는지 [lamb]가 벽에 이마를 기댔다."
                            "얼굴부터 뒷목까지, 머리카락 색처럼 물든 채로 간간히 '끄응...'하는 신음소리를 낸다."
                            "참으로 지루하고 평화로운 시간이 지나간다... ..."
                            "...... ......"

        "공부할 거야.":
            $ belief += 3
            $ change_image('say_2')
            T "어 그, 그러면 내가 너무 부끄러워지는데..."
            T "뭐를, 공부하는지 물어봐도, 돼?"

            menu:
                "살인을 해도 되는가.":
                    $ change_image('warry')
                    T "응...? 사, 살인은 안 되지. 아마 대부분의 종교가 하지, 하지 말라고 할걸...?"
                    T "우리 종교도... 그럴 거고..."
                    $ change_image('base_2')
                    T "굳이 종교가, 아니더라도. 살인은 하면 안 된다고, 암묵적으로 약속되어 있잖아. 왜냐하면 그, 그건, 인간성의 상실이니까."
                    T "... 인간성의 상실은 사회에서 낙오됨을 뜻하니까..."
                    $ change_image('eye_bottom')
                    T "...... 그렇지..."

                    menu:
                        "죽이고 싶은 존재가 있어?":
                            $ belief -= 4
                            # 완전 어두운...
                            # 이상한ㄷ가
                            $ change_image('black_3')
                            T "... ... 난 이미 몇 번이고 죽였어..."
                            T "목을 조르는 건 새, 생각보다 훨씬... 기분이 이상해."
                            T "고작 손가락으로, 죽어버리고... 그, 그렇게 연약한 부위를, 함부로 드러내도 되는 건가."
                            T "있지 [you]. 사람은 전부 눈을 뜨고 죽어? 내, 내가 다른 시체는 본 적이, 없어서..."
                            T "{i}형제{/i}는 죽을 때조차 날 응시했어."

                        "넌 이미 했잖아." if kill == True:
                            $ belief -= 5
                            $ change_image('base_2')
                            T "했다니... 뭐를?"
                            T "뭘, 죽였지? {i}형제?{/i} 하지만 그는 늘 살아나는걸..."
                            T "... 아. '그 애'를 말하는, 거야? 그건, 그거는... ..."
                            $ change_image('say_2')
                            T "'그 애'가 사람이야?"
                            T "아니, 그치만. 살인은 사람을 죽인다는, 뜻이잖아. 그, 그 애가 사람이었어?"
                            T "그리고 그때 너, 너도 같이 있, 었잖아. 왜 나만, 문제란 듯이 얘기해?"
                            T "난 그냥, 너를 따랐을 뿐이야. 내가 잘못했다면, 너도 잘못이잖아. 네, 네가 나의 선생님이니까."
                            T "... 혼자만 자유로워지지 마."

                        "똑똑하네.":
                            $ belief += 2
                            $ change_image('eye_right')
                            T "... 이런 지식이야 보통, 다 알고 있는걸... 별거 아니잖아."
                            T "너도 아, 알고 나를 놀리, 는 거지? 아... 어... 이게, 이게 아닌데."
                            $ change_image('panic_4')
                            T "이런 생각은, 안 되, 안 되는... 안 되던가...? 모르겠어..."
                            $ change_image('panic_8')
                            T "... 내가 말한 게 정말 오, 옳은 걸까?"

                    $ change_image('panic_6')
                    T "... 너무 어려운 이야기를, 하는 거 같아."
                    $ change_image('base_2')
                    T "난, 필요하다면 살인은, 할 수 있다고 새, 생각하지만... 그러면 안 된다는 것도, 알고 있어."
                    T "왜, 왜냐하면. 사회에 해로운 해, 행위니까. 인류의 종속을 위해선 그렇게, 해야 하니까."
                    T "그런데 만약에 그, 그분이 우리에게 학살을 명하면, 우린 무엇을 따라야 할까?"
                    $ change_image('say_2')
                    T "인간적인 약속과 그분의 뜻이 다르면 어느 쪽을, 선택해야 돼?"

                    menu:
                        "신의 뜻":
                            $ belief += 5
                            $ change_image('eye_bottom')
                            T "그렇지... 인간은 우리를 구원, 해주지 못하니까. 그게 당연, 한 거야."
                            T "어떻게 그, 그분의 뜻을, 우리가 이해할 수 있겠어. 우린 그저 따르기만 하면, 돼."
                            T "이제 사, 사람을 죽이는 건 두렵, 지 않아졌어. 아니. 처음부터 그런 건 아무 느낌도 없었어."
                            $ change_image('very_happy')
                            T "왜, 왠지 잘할 수 있을 것 같은 느, 느낌이 들어. 하하, 그분이 시키는 건 무엇이든 할 거야."

                        "인간성":
                            $ belief -= 3
                            T "그래... 그런 건 결국, 광기를 두른 짐승이 될, 뿐이니까... 짐승이 종교를 가질 수는, 없지."
                            T "우리는 인간으로 존재해야, 되는구나. 이, 인간성을 유지해야 돼."
                            T "신을 따르는 존재는 고결해야 하니까. 맞아. 그렇지."
                            $ change_image('sad_5')
                            T "그, 그런데 난, 인간이 아닌 거면, 어떡하지?\n이미 너무 마, 많은 것을 죽였, 어"


                "너의 증상에 대해서.":
                    $ belief -= 5
                    $ change_image('warry')
                    T "증상이라니. 내, 내가 병에 걸렸다는, 거야? 난 아프, 아프지 않아."
                    T "무서우니까 그런, 식으로 말하지 마..."

                    menu:
                        "망상이 심해.":
                            $ change_image('base_2')
                            T "망상...? 음, 음... 어, 어디서부터가 망상인지, 잘 모르겠어..."
                            T "망상의 정의가, 뭐였지. 어제, 어제 저녁이 맛있었다, 같은 것도... 망상이야? 그런 건 다들, 다르게 느끼잖아."
                            T "어쩌면 {은는}[you]{/은는} 취향과 경험의 차이를, 망상이라고 말하는 거 아냐?"
                            $ change_image('smail_15')
                            T "... 노, 농담이야. {을를}[you]{/을를} 따라해, 봤는데... 역시 시, 실패려나. 하하."
                            $ change_image('base_2')
                            T "나보단 네가, 더 옳겠지. 맞아. 나, 나도 날 못 믿어. 그러니 {이가}[you]{/이가} 나의 선, 생님인 거야."

                        "환청을 들어.":
                            T "그... 그건 그냥, 잘못 들은 걸 수도, 있잖아. 좀 시끄, 럽긴 해도..."
                            T "오늘, 오늘만이었으니까, 괜찮아. 아, 아프지 않아."
                            T "다음에도 그럴, 순 있지만 이런 거 그, 그렇게 중요하지, 않잖아. 응?"
                            $ change_image('say_2')
                            T "왜... 왜 이렇게, 불안하지... {이가}[you]{/이가} 나를, 잡아갈 거 같아..."

                        "없는 것을 봐.":
                            $ belief -= 3
                            # 뭔가 작게 흔들리면 좋겟다
                            # $ change_image('what')
                            hide warry
                            show what at shake
                            # 그냥 hpunch를 넣을까
                            T "그... 그만하라고 했잖아!"
                            $ change_image('panic_8')
                            T "헉... 아, 으으... 미, 미안. 아니, 아, 나 정말 무서워, 이 얘긴 하, 하고 싶지 않아..."
                            T "다들 똑같이 말해. 내 두려움 같은 건 생각도 안 하고, 존재하지 않는 것에 무서워하는 멍청이라고..."
                            $ change_image('sad_13')
                            T "너희들한텐 안 보여도, 나한테는 실제한단 말야... 지, 진리 같은 거, 중요하지 않아. 그냥 두려, 두려움에서 꺼내달라는, 것뿐인데..."
                            "이 안타까운 신도는 그 자리에서 엉엉 울기 시작했다. 조금 있으면 어른이 될 나이인데도 자신에겐 해당되지 않는다는 것 마냥."
                            "어깨를 들썩이고 히끅이며 중얼이는 말은 해석이 어렵다. [lamb]가 진정될 때까지 기다리자."
                            $ change_image('sad_5')

                    menu:
                        "미안해.":
                            $ belief += 3
                            T "응? 뭐, 뭐가...?"

                            menu:
                                "불편하게 한 것.":
                                    $ belief += 2
                                    $ change_image('eye_right')
                                    T "아... 그건... 으, 응. 괜찮아. 나아지진 아, 않았지만... 늘 이러니까, 괜찮을 거야."

                                "그냥.":
                                    $ change_image('eye_right')
                                    T "어... 그래.. 그, 그냥이라도. 조금, 기쁘네..."

                            T "미안하다고 들을, 줄은 몰라서... 고마, 고마워. 이게 고마운 게 맞는진, 모르겠지만."                            

                        "[lamb]를 안아준다.":
                            $ belief += 3
                            "마른 몸은 별다른 저항 없이 품에 안겼다."
                            "비록 {이가}[you]{/이가} 왜 안아주는 건지 이해하지 못해 혼란스러운 것 같으나..."
                            "그럼에도 [lamb]는 편안함을 느낀다."

                        "내버려둔다.":
                            $ belief += 0
                            "조금 시끄러웠을지 몰라도 시간은 금방 흘러갔다."

                    $ change_image('mung')
                    "여러 일로 힘이 빠진 건지 나른한 얼굴의 [lamb]가 바닥에 앉았다."

                "미래에 관한 공부.":
                    $ change_image('base_2')
                    T "미래... 머, 멋지다. 꿈이라도, 있는 거야?"
                    T "여, 옆에서 구경해도 돼? 방해하지, 않을게."
                    T "음, 음. 정확히 뭐를, 공부하는 거야? 아, 이게 방해인, 가."

                    menu:
                        "장래희망":
                            $ change_image('eye_bottom')
                            T "... 미래를, 생각할 수 있다는 건... 정말 부, 부러운 것 같아. 아니, 부러워."
                            T "되고 싶은 자신이 이, 있어. 계속 살아가려 하고... 대단해 [you]."

                        "삶의 방식":
                            T "바꾸고 싶은 거라도 있는, 거야? 아. 보, 보통은 성장, 이라고 말하던가. 나, 나도 그런 게 있어. 이미 알고 있겠지만..."
                            $ change_image('smail_1')
                            T "하하. 맞아. 나의 {i}형제.{/i} 그는 계속 내 곁에 있을 거야."
                            T "나는 부, 분명, 끝없이 너를 부러워, 하겠지."

                    $ change_image('smail_1')
                    T "변화를 바라잖아. 그럴 거라 믿고. 나도 너처럼 될 수 있다면, 참 좋았을 텐데."
                    T "내 남은 삶을, 더 살고 싶은, 다른 이에게 넘겨 줄 수 있다면 저, 정말, 좋았을 텐데 말이야. 그치."
                    $ change_image('eye_bottom')
                    T "그렇게라도 이런 생이 보람, 을 가질 수 있었, 다면... ..."
                    T "... 이런 이야기는, 아... 그래..."
                    $ change_image('smail_5')
                    T "너의 미래를 응원해. 진심으로."

                    menu:
                        "넌 그분을 섬기잖아.":
                            if love:
                                $ belief += 5
                                $ change_image('smail_14')
                                T "... 맞아 엘시. 나의 신. 너를 사랑해. 미, 믿어줘."
                                T "얼마, 안 남은 내게서... 가장 사랑에 가까, 운 존재는 엘시 너뿐이야."
                                T "어, 언젠가는 나 자신도 버려서, 오로지 너만을 사랑할게."
                                T "부디 자애를... 그때까지, 아주 조금만. 기다려 줘."

                            else:
                                $ belief += 3
                                $ change_image('smail_10')
                                T "하하... 으, 응. 이것도 정말 어, 엄청난, 보람이지..."
                                T "깨닫게 해 줘서, 고마워. 늘 {은는}[you]{/은는}, 앞에서 나를 이, 이끌어 주는구나."
                                T "죽을 때까지 그분을 섬, 겨야지. 이게 내게 유일하게 허, 허락된, 사명이야."
                                T "보잘것없어도 이 정도는 바, 받아주시겠지? 부디 그래주시면... 좋을 텐데."

                        "그럼 내게 줘.":
                            $ change_image('say_2')
                            T "그게 무슨..."
                            if lamb == '아그넬로':
                                if you == '엘시':
                                    $ belief += 5
                                    $ change_image('smail_5')
                                    T "나는 이미 에, 엘시의 소유잖아? 넌 내 모든 걸 손에 쥐, 었어."
                                    T "아. 아직 엘시를 위해, 죽을 각오는 없지만... 원한다면 충분히, 그렇게 만들 수 있잖아. 왜냐하면, 엘시는, 전능하니까."
                                    T "아니, 아니면. 내가 확신을, 주지 못한 거야? 오, 올바른 신도가, 아니라서? 미안해. 더 노력, 할 테니까. 날 버리지 말아 줘..."
                                    T "너의 어린양은 바보야. 그, 그러니까, 방법을 알려주면 전부, 따를게. 엘시만이 나의 길이고 정답이야. 엘시가 나의 진리야."
                                
                                else:
                                    T "... 난 이미 주인이 있어."
                                    T "신도를 찾는 거라면 나, 나 말고 다른 것을, 찾아."

                            else:
                                $ belief -= 2
                                T "이, 이상한 말이야. 왜 네게... ... 네가 뭐라고..."
                                $ change_image('what_2')
                                T "아, 아니! 잘못, 잘못 말한 거야! 이런, 이 말이 아니라, 어어..." with hpunch
                                T "이유, 이유를 못 찾, 아서... 그런 거니까...!"
                                T "{을를}[you]{/을를} 무시한 게 아니, 아니니까... 용서해 줘... 미안해."
                                $ change_image('smail_5')
                                T "가, 갖고 싶으면 가져. 나도 필요 없어. 하하."

                        "더 나아질 거야.":
                            $ belief += 4
                            T "... 정말, 그럴 수 있을까?"
                            T "그, 그랬으면 좋겠다. [you]의 말이니까, 믿을게."
                            $ change_image('eye_right_2')   
                            "시선이 한 바퀴 굴러 다시 [you]에게 닿았다. 은은히 미소지으며 뜸 들이던 입술이 열렸다."
                            $ change_image('smail_1')
                            T "... {은는}[you]{/은는}, 자신을 속하던 무언가를 잃어, 버린 적 있어?"
                            T "예, 예를 들면. 손가락 같은 신체, 의 일부라던가. 마음... 보통 말하는, 트라우마. 미각이나 촉각 같은 것. 더는 와닿지 않는 기쁨 같은 것 말이야."
                            T "다, 다들 한 번쯤은 겪는, 고통인 거지? 나만 겪는 게 아니, 잖아."
                            $ change_image('smail_5')
                            T "머리가 난도질 당한 기분도."
                            T "... {이가}[you]{/이가}. 이해해주면, 좋겠다."


                    $ change_image('base_2')
                    T "지, 지쳐서 그런데, 조금만 쉴게... 방해해서 정말, 미안해."
                    T "공부 히, 힘내."
                    $ change_image('eye_bottom')
                    "[lamb]는 그렇게 얌전히 앉아서......"

    $ change_image('eye_shut')
    "어느 순간, 벽에 기대 멍하니 있던 [lamb]가 꾸벅꾸벅 졸기 시작했다."
    "가라앉은 공기 속에 잔잔히 울리는 클래식이 자장가처럼 느껴지기라도 한 건지."
    "방 안은 나른함으로 가득 찼기에 어쩌면 [you]도 덩달아 졸릴지 모른다."

    menu:
        "쓰담쓰담":
            $ belief += 2
            show expression AlphaMask("black70", At(new, center)) as mask at multiply
            "자유분방하게 뻗친 머리를 손바닥으로 감싸면 복슬복슬한 감촉이 피부를 간지럽힌다."
            "[lamb]는 작게 움찔거렸지만 깨진 않았다. 오히려 [you]의 손에 슬쩍 머리를 기댔고..."
            "꽤나 편해 보인다."
            "자고 있는 사이에 마음껏 머리를 쓰다듬었다."

        "찰싹찰싹":
            $ belief += 1
            # $ change_image('eye_shut')
            show eye_shut at shake
            "장난 같은 소리가 날 만큼의 세기로 양 뺨을 두드린다. 손 끝이 닿을 때마다 볼살이 살짝 흔들렸다."
            T "으응... ..."
            "음... 이 정도 세기로는 [lamb]가 일어날 것 같지 않다."

            menu:
                "더 세게 때릴까?{fast}"
                "좋아.":
                    $ belief -= 2
                    "처음보다 강하게, 양 뺨을 두드린다. 아까보다 더 큰 소리가 났다."
                    # 왔다갔다 모션을 넣어말아
                    show eye_shut at shake
                    "규칙적으로 밀리던 머리가 양 손 사이에서 왔다갔다 하는 모습이 우스꽝스럽다."
                    # "농구공 같을지도"
                    $ change_image('sleep')
                    hide eye_shut
                    T "읏... 아, 머, 뭐야...??"
                    "이리저리 머리를 흔들던 [lamb]가 드디어 일어나 어지러운 머리를 붙들었다!"
                    $ change_image('panic_6')
                    T "머... 멀미가... 으으... 나 잠들, 잠들었던 거야? 많이 기다렸어?"
                    T "기다려, 줄 거라고 생각 모, 못했는데... 기, 기뻐. 그리고 아파..."

                    menu:
                        "나가자.":
                            $ change_image('base_2')
                            T "얼마, 나 있었던 거지. 시, 시계가 없어서..."

                            $ change_image('smaile_14')
                            T "창 밖은... 노을이, 되고 있네. 엄청 오래 고, 공부했다. 그치."
                            T "시험... 잘 보면 좋을 텐데. {은는}[you]{/은는} 괘, 괜찮아. 나보다 훨씬 똑똑, 하니까. 한 번에 끝내잖아. 그치?"
                            $ change_image('base_2')
                            T "이제 가, 갈까. 아직 잠이 덜, 깼나 봐. 어지럽고 졸려..."

                        "배고파.":
                            $ change_image('base_2')
                            T "아 시간이 벌써... 으응. 슬슬 밥 먹을, 시간이구나. 미안 기, 기다리게 했네."
                            T "{은는}[you]{/은는} 무슨 요리, 좋아한다고 했었지? 나, 나는 토마토 요리를 좋아, 해. 전에 말했던가?"
                            "꼬르륵......"
                            $ change_image('eye_right_3')
                            T "헉... 웨, 웬일로 배가 고프, 고프지. 하하. 부끄럽게..."
                            T "어, 어서 가자...! 오, 오늘 메뉴 기, 기대된다. 그치."

                        "그냥 간다.":
                            $ change_image('what')
                            T "가, 가는 거야? 같이 가...!"
                            "망설임 없이 밖으로 향하는 [you]의 뒤를 [lamb]가 황급히 쫓는다."

                    hide panic_6
                    scene black
                    with dissolve

                    "고요했던 방 안과는 다르게 밖은 어수선했고 다양한 공기가 피부를 스쳐지나가며 세상에 남은 것이 둘만이 아니라는 사실을 증명했다."
                    "문을 다시 닫으면, 그 안엔 무엇이 남을까?"
                    "이제 헤어질 시간이다. 오늘 하루도 많은 수고를 했으니 어서 돌아가 기운을 충전하자."
                    "좋은 저녁 되기를."

                    $ day += 1
                    jump room

                "관둔다.":
                    "많이 피곤했던 모양인지 꽤 깊게 잠든 모습을 보니 일어나려면 시간이 좀 걸릴 듯싶다."
                    "그동안 무얼 하는 게 좋을까."

        "내버려둔다.":
            show expression AlphaMask("black70", At(new, center)) as mask at multiply
            "방에는 색색거리는 숨소리와 서서히 죽어가는 클래식 소리만이 울렸다."
            "{이가}[you]{/이가} 무얼 하든 시간은 흘렀고, 계속 흐른다."
            "지루하진 않는가? 이런 기다림보단 다른 것을 하는 게 더 좋지 않을까?"
            "눈앞에는 무방비한 [lamb]가 보이고..."

    menu:
        "어깨를 빌려준다.":
            $ belief += 3
            "살며시 머리를 끌어 어깨 위에 얹었다. 묵직하고, 따뜻하면서 여기저기 뻗쳐있는 머리카락이 목덜미를 간지럽힌다."
            "이렇게 가까운 거리에선 싫어도 서로의 체향을 느낄 수 있다. 다행히 [lamb]에게 끔찍한 악취는 풍기지 않았다. 오히려 존재감 만큼 희미하다."
            "규칙적인 숨소리가 귓가에 울린다."

        "무릎을 빌려준다.":
            $ belief += 3
            "쓰러지지 않도록 주의하며 살며시 머리를 끌어 무릎 위에 얹었다. 묵직하고, 따뜻하면서 여기저기 뻗쳐있는 머리카락이 다리를 간지럽힌다."
            "위에서 내려다본 [lamb]의 자는 얼굴은 평온하다. 딱딱한 바닥에 누워있음에도 마치 침대 위에 몸을 뉘인 것만 같다."
            "숨을 쉴 때마다 흉부가 오르락내리락하는 모습이 보인다."

        "버리고 간다.":
            $ belief -= 2
            "{은는}[you]{/은는} 자고 있는 [lamb]를 두고 가기로 결정했다."
            "당연하다. 이곳은 더없이 지루한 장소이고, 일어나지도 않는 사람을 깨우는 것도 귀찮은 일이니. 자리를 떠나 다른 일을 찾는 것이 현명한 판단이다."
            "어차피 오늘의 할 일도 끝났다. 더는 [lamb]의 곁에 있을 이유도 남지 않았다."
            "그러니 남은 하루 자유 시간을 즐기도록 하자. 좋은 밤 되기를."

            hide eye_shut
            scene black
            with dissolve

            $ day += 1
            jump room


    menu:
        "쓰다듬는다.":
            $ belief += 3
            "손을 뻗어 머리를 쓰다듬는다. 손길이 스칠 때마다 이마가 살짝씩 드러나 감겨있는 눈이 보였다."
            "속눈썹, 콧대, 작게 열려있는 입... 눈 밑에 자리잡은 다크써클. 창백한 피부."
            "[lamb]의 체온이 그가 아직 살아있다는 걸 알려주었다."

        "자장가를 불러준다.":
            $ belief += 4
            # $ change_image('eye_shut')
            "작게 노래를 흥얼거렸다. [you]의 노래 실력이 어떤지는 모르겠으나 어느 쪽이든 듣고 있는 [lamb]는 편안해 보인다."
            "지금 부르는 노래는 누구에게서 배웠는가? 처음 들은 자장가는 어땠어? 비록 답은 들을 수 없어도 한 번쯤 생각해 보는 것도 나쁘지 않을 것이다."

    hide mask
    $ change_image('mung')
    "십여 분 정도 지났을 때 몸을 움찔이며 [lamb]가 깨어났다. 잠에 취한 눈을 끔뻑이며 멍하니 허공을 응시하다가 순간 몸을 굳히곤 천천히 [you]에게서 떨어졌다."
    $ change_image('warry')
    T "... 어... 나 자고, 자고 있었... 어, 얼마나 잔 거지. 많이 기다린, 거야 [you]?"
    T "어떡, 아, 미안해. 나도 모르게, 그, 졸려서... 기, 기다려줘서, 고마워."
    T "오랜만에, 잘 자버려서... 여, 역시 곁에 누가 있어야 잘 자는, 자나 봐. 아니 이 말이 아, 아니라."
    $ change_image('base_2')
    T "시간이 얼마나 지났지? 이제 저, 저녁 먹을 시간이야. 많이 배, 배고프지..."

    menu:
        "괜찮아.":
            $ belief += 3
        
        "배고파. 책임져.":
            $ belief += 2

    T "으응, 미안... 어서 가, 가자."
    scene sacBg1 with dissolve
    "사이좋게 서로의 손을 잡고 방을 나선다. 고요했던 방 안과 다르게 복도 끝에서부터 왁자지껄한 소리가 벽을 타고 들려왔다. "
    T "이, 이렇게 떠들면 선생님한테 혼날, 텐데... 무슨 일이라도 있, 있나?"
    $ change_image('smail_5')
    T "굳이 신경쓸 필요는... 없겠지? 헤헤. 우리도 빨리, 가자."
    T "... 있지, [you]. 나랑 가, 같이 있어줘서 정말 고마워."
    T "일어났을 때 {i}형제{/i}가 아니라 네, 네가 보여서. 안심했으니까... 내가 꿈에서도 너를 찾은 줄 알았어."
    $ change_image('smail_1')
    T "이상한, 말이려나. 그냥 무시해 줘."
    hide smail_5 with dissolve
    "두 사람 분의 발걸음이 식당으로 향한다. 오늘의 하루는 이게 끝이다. 이제 헤어질 시간이니..."
    "건강하게, 내일 보기를. 잘 자렴."


    $ day += 1
    jump room


label dance:
    
    $ change_image('smail_1')
    T "{이가}[you]{/이가} 하고 싶은 포지션으로 맞출게. 따, 딱히 가리지 않고, 배웠거든. 왜였는진, 잘 기억 안 나는데... 아마 다, 잘 하고, 싶었을 거야."
    "[lamb]의 손과 [you]의 손이 느리게 얽혔다. 엮인 손가락 마디에서 서로 다른 체온이 피부에 스며든다."

    menu:
        "[lamb]의 어깨 위로 손을 올린다.":
            $ change_image('smail_9')
            "활짝 핀 손바닥이 [you]의 허리를 감싸고 남은 손은 [lamb]의 어깨 위에 올라갔다."
            "오랜만이라 걱정된다던 말과는 다르게 매끄러운 움직임으로 느리게, 확실히 춤을 이어나간다."
            "[lamb]는 제대로 {을를}[you]{/을를} 리드하고 있었다."

        "[lamb]의 허리를 감싼다.":
            $ change_image('smail_9')
            "빈약한 허리를 감싸면 살포시 [you]의 어깨 위로 손이 올라왔다."
            "서로의 스텝에 주의하며, {이가}[you]{/이가} 이끄는 대로 [lamb]의 몸이 뒤따라간다."
            "느리지만 분명하게, 둘은 춤을 이어나가고 있었다."

    "리듬을 타고 흔들리는 몸을 따라 둘의 옷자락이 나풀거린다."
    "발을 옮길 때마다 미지근한 공기가 뺨을 스쳤다."
    "앞에 보이는 창백했던 얼굴은 열이라도 오른 건지 발갛게 달아올라서..."

    $ change_image('smail_14')
    T "... 어, 어때?"

    menu:
        "뭐가?":
            T "그냥... 즈, 즐거운지 궁금했을 뿐이야. 난 무척 긴장되고 두려운데..."
            T "{이랑}[you]{/이랑}, 함께여서 그런가. 마, 많이, 기분 좋아. 이게 즐, 거움일까?"

        "좋아.":
            $ belief += 3
            $ change_image('smail_7')
            T "정말? 네가 그렇다면 나, 나도... 좋아. 즐거워. 아마, 응."
            T "함께 할 수 있어서 기뻐..."

        "지루해.":
            $ belief -= 1
            $ change_image('smail_1')
            T "그... 렇구나. 그럼 어쩔 수, 없지."
            T "지루해도 어울려줘서, 고마워. 난 기분 좋아."  # 이쯤되니 점점 뻔뻔해진다

    "이제 턴을 할 차례다. [you], 너는 이 춤의 순서를 알아?"
    "네가 알았던 몰랐던... 처음부터 즉석인 춤이었고, 합을 맞춰본 적도 없으니 실수를 해도 이상하지 않다."
    "이윽고 둘 중 하나가 몸을 떨어뜨렸고, 맞잡은 손을 위로 올리고, 몸을 틀어서......"
    $ change_image('panic_6')

    window hide # 대사창을 닫음!!
    # 마우스 좌클릭 불허 설정
    $ use_click(False)
    # 2초를 기다림!!
    pause(2)    
    # 0.1초뒤에 마우스 좌클릭 허용 설정
    $ threading.Timer(0.1, use_click, [True]).start()

    $ danceing = renpy.random.randint(1,2)
    if danceing == 1:   #발밟기
        $ change_image('panic_2')
        "... {i}콱!{/i} 으악!" with hpunch
        "이럴 수가! [lamb]의 발이 무참히 밟혔다!"
        "휘청이던 몸의 중심을 잡기 위해 일어난 불상사다. 어쩔 수 없다. [lamb]는 춤을 멈추고 자리에 주저앉아 발을 감싸쥐었다."
        "눈물이 찔끔 나지만 괜찮아 보인다."
        T "아, 아파... 아니, 어, 난 괜찮아. 괜찮아, 괜찮아..."
        "아쉽지만 춤은 이렇게 마무리 되었다. [lamb]가 나아질 때까지 조용히 기다리자..."
    else:
        $ change_image('smail_14')
        "... 믿을 수 없을 정도로 완벽하고 안정적이게 턴을 마쳤다!"
        "분명 같이 하는 건 처음일 텐데, 둘이 그만큼 친해진 걸까? 우리 친구야?"
        "마무리 인사까지 제대로 마치고, 흥분에 귀까지 빨갛게 물들인 [lamb]가 허리를 숙인 채 숨을 몰아쉬었다."
        $ change_image('smail_13')
        T "대, 대단해...! 어머, 어머니 외의 사람이랑, 이렇게 춘 건 처음이야. 아, {은는}[you]{/은는} 저, 정말 뭐든, 잘 하는구나. 무척 즐거웠, 어. 그러니까, 어, 행복했다고..."
        $ change_image('smail_7')
        T "조금 부끄, 러웠는데 같이 하, 하니까 좋다. 집중하느라 혀, 형제도 안 보이고. 조금 어지럽긴 했지만... 그래도 정말, 난, 진심으로... 그게..."
        $ change_image('smail_1')
        T "미, 미안, 말이 너무 많, 았지... 이런 기쁨은 오랜, 만이라 그랬어."

        menu:
            "계속 말해도 돼.":
                $ belief += 3
                T "으응, 아니야. 이, 이제 흥분도 식었고... 말하고 싶은 만큼, 충분히 말했어."
                T "함께 해 줘서 고마워, [you]."

            "나도 즐거웠어.":
                $ belief += 2
                $ change_image('smail_2')
                T "우리 다, 다음에도 다시, 할 수 있을까?"
                T "오늘 같은 날이 또 올지는 모르, 겠지만..."
                T "다음엔 좀 더 고, 공부해서, 더 잘하고 시, 싶어."

            "이제 쉬자.":
                $ belief += 1
                T "[you]도 히, 힘들었구나. 으응 누, 눕는 게 나으려나. 누우면 혼날까..."

    "둘은 얌전히 자리에 앉아 휴식했다. 짧게 춘 춤이지만, 그래도 기운을 빼기엔 좋았을까."
    















