# 5 외출

# 이미지
image outBg1 = "images/backgrounds/outBg/outBg-1.png"      # 거리1
image outBg2 = "images/backgrounds/outBg/outBg-2.png"      # 거리2
image outBg3 = "images/backgrounds/outBg/outBg-3.png"      # 놀이동산1
image outBg4 = "images/backgrounds/outBg/outBg-4.png"      # 연극
image outBg5 = "images/backgrounds/outBg/outBg-5.png"       # 돌아가는 길1
image outBg6 = "images/backgrounds/outBg/outBg-6.png"       # 가게
image outBg7 = "images/backgrounds/outBg/outBg-7.png"       # 안씀
image outBg8 = "images/backgrounds/outBg/outBg-8.png"       # 취객
image outBg9 = "images/backgrounds/outBg/outBg-9.png"       # 놀이동산2
image outBg10 = "images/backgrounds/outBg/outBg-10.png"       # 놀이동산2

# 순종과 벌
label outing:
    scene outBg1 with dissolve

    $ change_image('hi_1')
    T "아, 안녕! 이쪽으로 와."
    T "바깥, 은 무척 오랜만이야. 헤헤."
    menu:
        "밖이 좋아?":
            $ belief += 3
        "신나보이네.":
            $ belief += 2
    
    $ change_image('smail_3')
    T "응... 밖은 실내보다 너, 넓으니까, {i}형제{/i}도 멀리, 있어서, 조, 좋아해."
    T "너도... 외출하는 거, 좋지...?"

    menu:
        "얼굴이 기대로 가득한 [lamb]에게 뭐라고 답할까?{fast}"
        "응. 좋아해.":
            $ belief += 2
            $ change_image('smail_2')
            T "...! 그치, 히히. 나, 나가는 거, 어렵잖아. 특히 나는 믿음, 이 부족하다고, 못 나가게 했어."
            T "그래서 수업이 끝나면 늘 선생님이랑 둘이... ..."
            $ change_image('tension_1')
            T "... ... 어라."
            T "뭐였더라... 기, 기억이 안 나네. 으음, 중요한 건 아, 아닐 거야."

        "아니.":
            $ belief -= 2
            menu:
                "성전이 더 좋아.":
                    $ belief += 7

                "너랑 나가는 거 싫어.":
                    $ belief -= 7

                "추워.":
                    $ belief -= 1

            $ change_image('tension_1')
            T "아... 그, 그래도 오늘만 참아줘..."
            T "{이랑}[you]{/이랑} 있는 거여서 특별히 허락, 해준 거란 말야."
            T "혼자서는 못 나가니까..."

    $ change_image('smail_1')
    T "그보다 있지, 어제 눈이 내렸나 봐. 창문 틈으로 내리, 는 걸 보긴 했는데, 엄청 쌓였네."
    T "미끄러지지 않게... 조심해, [you]."
    $ change_image('base_2')
    T "... 그으, 사실 나, 가고 싶었던, 곳이 있었거든."
    T "유, 유치하다고 놀리지 말아 줘. 근처에서 유원지가 열린대. 너랑, 정말 가고 싶, 싶어서... 선생님한테, 졸랐어."
    $ change_image('warry_2')
    T "그러니까 제발... 나랑 가, 같이 가면, 안 돼...?"

    menu:
        "[lamb]가 간절하게 눈을 맞췄다.{fast}"
        "같이 갈게.":
            $ belief += 5
            $ change_image('smail_6')
            T "...! 으, 응! 고마워. 고마워 [you]. 어, 엄청 기분이 좋아. 히히..."
            T "기, 기분, 이 좋아서 그런데, 한번, 한번만 {을를}[you]{/을를} 안아줘도 돼?"

            menu:
                "안돼.":
                    $ belief -= 3
                    $ change_image('base_2')
                    T "아... 응... 알았어. 안 할게..."

                "그래.":
                    $ belief += 3
                    $ change_image('smail_2')
                    T "아...! 너, 너는 정말 착한 친구야... ... 우, 우리 친구 맞겠지...?"
                    "[lamb]가 [you]의 몸을 조심스럽게 꼬옥 끌어안았다."
                    T "정말 좋아해... 아, 아마도."

        "싫어.":
            $ belief -= 5
            $ change_image('what')
            T "시, 싫어...?\n왜...?"
            T "같이, 같이 놀면, 좋잖아. 아, 음, 왜 싫어...?"
            $ change_image('sad_1')
            T "거짓, 거짓말이지? 농담한 거지 [you]...?"

            menu:
                "맞아. 농담이야.":
                    $ belief += 3
                    $ change_image('smail_8')
                    T "그치? 그, 그럴 거라 믿었어. 깜짝, 깜짝 놀랐잖아. 그런 노, 농담은 싫어..."

                "아니. 싫어.":
                    $ belief -= 5
                    T "아... ..."
                    $ change_image('eye_bottom')
                    T "...... ......"
                    T "그렇구나..."
                    T "그럼, 어쩔 수 없지..."
                    $ change_image('sad_4')
                    if lamb == "아그넬로":
                        "어린양이 눈물을 떨군다. 얼굴에 담긴 감정은 한없이 고요하다."
                    else:
                        "[lamb]가 조용히 울었다."
                    T "... 할 일이 사라졌네. 오늘은 나 혼자 놀게. 귀찮게 해서 미안해."
                    T "오늘 만나서 즐거웠... 다고 말하기도 애매하지만... 널 만나서 좋았어."
                    T "조심히 들어가. 안녕."
                    hide hide_image
                    scene black
                    with dissolve

                    $ day += 1
                    jump room

        "같이 가면 뭘 줄 건데?":
            $ belief += 2
            $ change_image('base_2')
            T "어, 뭐, 뭘 줘야 해...?"
            T "어쩌, 어쩌지. 용돈도 얼마 없는데..."
            T "네가 뭘 워, 원하는지 모르겠어... 나, 나는 줄 수 있는 게, 정말로 없어서..."
            T "지, 지금 말고, 일단 갔다 온 다음, 다음에 말해 줘. 웬만하면 다 줄 테니까."

    T "[you], 파, 팔을 안고 가도 돼? 너한테서 버려지고 싶지 않아..."

    menu:
        "안지 마.":
            $ fal = False
            $ belief -= 2
            T "으, 아, 알았어, 미안..."
            "시무룩한 [lamb]를 무시한 채 어느덧 유원지에 도착했다."
        
        "그래, 이리 와.":
            $ fal = True
            $ belief += 2
            $ change_image('smail_7')
            T "고, 고마워, 이번만 아, 어리광 부리게 해 줘."
            "[you]의 팔이 [lamb]의 품에 갇혔다."
            "은은한 체온이 전해진다."
            T "따뜻... 해서 좋다."
            "노곤노곤한 표정을 짓는 [lamb]와 함께 어느덧 유원지에 도착했다."
    
    scene outBg9 with dissolve
    "소박한 유원지. 작지만 다양한 놀이기구가 빛을 번쩍이며 움직인다. 은근히 많은 사람들의 웃는 소리가 들렸다."
    "밤에는 서커스도 한다지만, [lamb]는 시간이 허락되지 않았기에 볼 수 없다."
    "하지만 너와 함께라면 혼날 각오를 하고 일탈을 할 수도 있어."
    $ change_image('smail_1')
    T "[you], 어, 어디로 갈까? 해보고 싶은, 거 있어?"
    T "나는 음, 대관람차, 를 타고 싶었는데... 여긴 작은 유, 유원지라 그런 건 없나 봐."
    T "놀이기구는... 아이들이 타도록, 양보해야겠다. 이, 이젠 다 컸으니까."

    menu:
        "타고 싶어?":
            $ belief += 1
            T "응? 아, 아니야. 어차피 아이들용이고... 우리가 탑승하면 어, 엄청, 낄걸."
            T "기구, 가 망가지면 혼날 거야..."
            $ change_image('base_2')
            "[lamb]가 불안하게 주위를 두리번거린다."

            menu:
                "누구에게 혼이 나는데?":
                    $ belief += 5
                    T "음, 음, 아버지...? 아니면 서, 선생님이라던가... 아무튼 어른, 들이..."
                    T "그것, 그것도 아니라면, 신 님이라던지."
                    "어딘가에 들리면 안 된다는 듯 바싹 몸을 붙여 네게 속삭였다."
                    T "그, 그분은 쉽게 벌을 주셔. 나는, 난, 그게 너무 두려워."
                    T "내가 이, 이런 얘기한 거, 그분에게 절대, 절대 이르지 말아줘, [you]. 알았지...?"
                    T "널 믿으니까... ..."

                "저기 어른들도 타고 있어.":
                    $ belief += 2
                    T "아... 그치만 저거, 저건 아이들을 살피려고, 같이 타는 거잖아."
                    T "나, 나도 그 정도는 알아."
                    T "으음... 네 말대로 우리, 가 탔다고 기구가 망가지진, 않겠지만..."
                    T "난 그냥 양보할래... {color=#2E2E2E}모두 없어졌을 때 타고 싶어.{/color}"

        "너도 애야.":
            $ belief += 3
            T "어, 뭐라고? 하하. 아, 아직 애인 건 맞지만... 으응, 너무 커버렸잖아."
            T "18살이니까, 조금만 더 지나면 어른이 돼."
            $ change_image('base_2')
            T "... 어른이 되어버리네. 되고 싶지 않아..."
            T "그, 그야. 어른은 아무, 도 책임지지 않잖아. 버려질 거야."
            # 이상하면 빼버려
            # 귀에 피남 ㅠㅠ 고쳐
            $ change_image('sad_2')
            T "나에겐 정말로 신 님밖에 없어... ..."

            menu:
                "나도 있어.":
                    if love:
                        $ belief += 7
                        $ change_image('smail_8')
                        T "... 맞아, 내겐 너, 너밖에 없지..."
                        T "같이, 함께할 수 있어서. 이, 이게 행복이겠지?"
                        T "이곳에 너와 서 있을 수 있어서 기뻐."
                        T  "... 고마워 엘시."
                    else:
                        $ belief -= 5
                        $ change_image('smail_1')
                        T "너...? 하. 하하. 별로 기대하진 않아."
                        T "가족도 나를 버, 버리는데, 네가 뭐라고 나를, 날 책임져?"
                        T "내가 아무, 리 멍청하다지만, 그러, 그런 거에 희망을 품을, 정도로 바, 바보는 아니야."
                        T "분명 너, 너도 날 버릴 거야. 확신해. 이, 이런 애를, 누가 좋아하겠어."
                        T "내기 할까? {이가}[you]{/이가} 이기면, 어... 가진 게 어, 없네. 나를 줄게. 필요 없으려나..."
                        T "... ..."
                        $ change_image('base_2')
                        T "... 갑자기 흥분해서 미안, 해. 음, 음, 미안... 자제할게..."

                "그럼 그분에게 더 잘해야겠네.":
                    $ belief += 5
                    $ change_image('base_2')
                    T "[you] 말이 마, 맞아. 잘, 할려고 노력하는데 늘 모, 못해... 혼나, 기만 하고..."
                    T "항상 부족하대. 아, 그치만 이런 말도 죄, 죄라고 했으니까... 생각도 조심, 해야 돼."
                    T "나에겐 너, 너무, 어려워. 열심히 하는데..."
                    T "... 다 내가 잘못이겠지."
            
        "불쌍한 [lamb].":
            $ belief -= 5
            $ change_image('warry')
            T "... 왜, 왜 갑자기 그런, 말을 하는지 모르겠어."
            T "뭐가, 불쌍하다는 거야...?"
            T "그런 말은 싫어... 나는, 괘, 괜찮아. 괜찮아야만 돼. 불쌍하지 않아."

            menu:
                "놀이기구도 못 타니 불쌍하지.":
                    $ belief -= 3
                    $ change_image('what')
                    T "그, 그렇게 불, 쌍하진 않아. 어릴 때도, 못 타긴 했지만..."
                    T "... 아버지가. 이런 곳을 싫어하셨어. 그나마 어머니가 그네를 밀어주셨지."
                    T "아...! 이, 이런 얘긴, 재미 없지? 미, 미안..."
                    T "너에게 우리 부모님을 흉본 건 아니야... 그냥, 나도 한 번쯤은 와보고, 싶었어."
                    $ change_image('base_2')
                    T "그러, 니까. 못 타도 괜찮아. 난 불쌍하지 않아..."

                "어울리지도 않는 장소에 왔어.":
                    $ belief -= 5
                    $ change_image('sad_5')
                    T "그런... 그렇게까지, 말할 필요는..."
                    T "저, 정말로, 여기 오면 안 됐던 걸까? 아, 아냐. 아냐."
                    T "나도, 난, 여기 올 수 있어. 나도 여, 여기 와도, 괜찮아."
                    T "더는 말, 하지 마... 난 정말, 너랑 놀, 놀고 싶어, 서 왔단 말야..."
                    "[lamb]가 거칠게 소매로 눈가를 닦았다."
                    $ change_image('sad_7')
                    "눈가가 발갛게 올라왔지만 무시하자."

                "여긴 왜 온 거야?":
                    $ belief -= 7

                    menu:
                        "아무것도 안 할 거면서.":
                            $ belief -= 3
                        
                        "너랑 있으면 짜증나.":
                            $ belief -= 5

                        "이런 곳은 신 님이 좋아하지 않아.":
                            $ belief += 3

                    $ change_image('sad_5')
                    T "그, 그치만... 한 번만 봐 줘, 이번에 오, 오지 않으면, 다음은 없을, 것 같아서..."
                    T "기분 나빴으면 미안해, 하, 하지만, 너도 같이, 온다고 해 줬으니까..."
                    T "조, 조금만, 참아 줘. 미안해. 미안해. 고마워."
    
    $ change_image('say_2')
    "[lamb]가 급하게 주위를 두리번거리더니 한 곳을 가리켰다."
    T "저기, 저기에, 연극을 한대. 저거 보러 가, 가자."

    hide hide_image with dissolve
    scene outBg4 with dissolve

    if fal == True :
        "[lamb]에게 팔이 안긴 채로 이끌리며 도착한 곳은 작은 천막."
    else:
        "[you]의 소매를 약하게 잡아당기며 [lamb]가 이끈 곳은 작은 천막."

    "내부는 어두웠고, 듬성듬성 앉은 아이들이 무대의 시작을 기다리고 있다."
    T "이, 이쪽으로 와."
    "우린 다 컸으니 앞을 가리지 않도록 뒤에 앉자며 속삭인 [lamb]를 따라 뒤쪽 구석에 앉으니, 곧 연극이 시작된다는 알림이 울렸다."
    $ change_image('smail_3')
    "[lamb]가 드물게 눈을 빛내며 {을를}[you]{/을를} 바라봤다."
    T "연극은, 어릴 때도 본 적 있는데... 그 당시엔 너무, 어려웠어. 셰익스피어라던가, 톨스토이라던가..."
    T "열 살도 안 된, 애한테 보여주기엔 참 보, 복잡한 이야기인, 것 같아."
    $ change_image('smail_1')
    T "... 그냥 내가 바보인가."
    T "[you]는... 연극 같은 거, 본 적 있어?"

    menu:
        "있어.":
            $ belief += 2
            T "우, 우와. 뭐 봤는지 물어봐도, 돼?"
            menu:
                "어린이 연극":
                    $ belief += 2
                    $ change_image('smail_2')
                    T "히히, 어, 어린 {은는}[you]{/은는} 어땠을까. 으음 사실 별로 궁금하진 않아..."
                    T "우리가 어릴 때부터 봤다고 해도, 달라질 것도 없겠지."

                "교리 해석 연극":
                    $ belief += 6
                    T "멋지다... 나, 나도 나중에 보고 싶어. 어디서 보, 보는 거야?"
                    T "다음에 나도 데, 데려가 줘. {이랑}[you]{/이랑} 있으면 선생, 선생님도 허락해주실 거야."

                "묻지 마.":
                    $ belief -= 3
                    $ change_image('base_2')
                    T "아, 으응, 미안..."
        "없어.":
            $ belief += 2
            $ change_image('base_3')
            T "아... 그럼, 이게 처음이야?"
            $ change_image('smail_3')
            T "너, 너의 처음을 내가 함께한다고 생각, 하니까 뭔가, 뭔가 기분이 좋아. 헤헤."

    T "... 어, 이제 시작하나 봐."

    hide smail_2 with dissolve
    hide smail_3 with dissolve
    hide base_2 with dissolve
    # 머 삐에로 png라도 넣을까
    # 탁~! 소리
    "흥겨운 음악이 연주됨과 동시에 무대 중앙에 빛이 비쳤다."
    "{i}옛날옛날 어느 마을에 부모님이랑 같이 사는 소년이 있었어요.{/i}"
    "{i}부모님이 소년에게 일을 하는 동안 빵을 잘 보고 있으라 했어요.{/i}"
    "{i}소년은 안 보고 놀았어요.{w}\n빵은 새들이 다 먹었답니다.{/i}"
    "아이로 분장한 건장한 청년이 과장되게 놀란 표정을 지으며 무언가에 쫓기는 장면이 나온다."
    "뒤이어 남자와 여자가 잔뜩 화가 난 얼굴로 청년을 쫓았다. {i}잘못했어요~!{/i}하는 비명이 들린다."
    "{i}소년은 혼났어요. 이번에 부모님은 비가 오니 물가에서 놀지 말라고 했어요.{/i}"
    "{i}또 무시하고 놀러 갔어요.{w}\n불어난 물에 휘말려 죽을 뻔했답니다.{/i}"
    "아이로 분장한 청년이 엉엉 울고 옆에서 부모가 청년을 타박하는 장면이 나왔다."
    "{i}부모님은 다시 소년에게 주방 불을 잘 지키고 절대 불에 술을 부으면 안 된다고 말했어요.{/i}"
    "{i}이번엔 지켰을까요? {b}그럼요!{/b} {w}소년은 불에 술을 부어버렸어요.{/i}"
    "{i}치솟은 불길이 집을 태워버리고 그 안에 있던 소년은 죽어버렸답니다.{/i}"
    
    play sound "audio/sound/짝짝짝.mp3"

    "연극은 '부모님의 말을 잘 듣자'는 교훈을 보여줬다."
    $ change_image('smail_1')
    T "아, 아이들을 겨냥한, 좋은 이야기네. 그치..." # 진짜
    "만족스럽게 박수를 치고 모두가 떠난 이후에도 [lamb]는 일어나지 않았다."
    $ change_image('base_2')
    T "그런데 왜... 부모님은 계속, 이유를 말해주지 않은 걸까."
    T "응? 아, 미안...그, 그냥, 이해가 안 가서. 이해하는 게 많은 것도 아니지만..."
    T "이유를 말해주지 않고, 그저 따르라고만 하니까... 까먹어 버리면, 또 혼나잖아."
    T "마, 말이 너무 많았네. 기다리게 해서 미안, 미안해. 일단 나가, 나갈까."

    menu:
        "그래.":
            $ belief -= 0

        "더 얘기해.":
            $ change_image('warry')
            T "어, 어...??"
            T "아, 나, 나는 그냥......"

            if love:
                # 시선 피하기이
                T "... 네, 네게 불만을 토한 건, 아니었어. 이런, 이런 식으로 은근슬쩍 말, 하려던 건..."
                T "아, 아니, 이러니까 괜히 혼자 찔, 린 것 같네. 아냐, 아니야."
                # 질끈?
                T "나는 에, 엘시의 말을 따르는 걸 저, 정말 좋아해. 진심이야. 사명이라고 생, 각할 정도로... 주, 죽을, 죽은 후에도 따르고 싶어."
                T "이, 이게 대체 무슨 말이지? 미안, 이만 나가, 는 게 좋겠다..."
                # 오른ㄴ쪽으로 나가기
                "[lamb]가 도망치듯 자리에서 벗어났다."

            else:
                $ change_image('base_2')
                T "... 집단마다 아주 많은 규칙이 있는데, 처음부터 전부 기, 기억하긴 어렵잖아. 이해도, 안 되고."
                T "그게... 조금, 불만이었나 봐."
                $ change_image('smail_10')
                T "... ... 아, 아무것도 아냐! 가자..."

    scene outBg2 with dissolve
    play sound "audio/sound/바람.mp3"

    # 움찔을 넣을까
    $ change_image('panic_2')
    T "으, 춥다..."
    $ change_image('base_2')
    T "저기 [you]. 나 조금, 배고픈데, 저, 저쪽에서 뭐 좀 사 먹어도 돼...?"

    menu:
        "간식들을 파는 가판대가 보인다.{fast}"
        "안 돼. 굶어.":
            $ belief -= 3
            $ change_image('warry')
            T "앗, 아, 안돼...?"
            T "[you], 나 뭔가 잘못, 잘못했어?"
            T "어... 잘 모르, 겠는데... "
            T "아... 알았어... 참아볼게..."

        "그러던가.":
            $ belief += 1
            $ change_image('smail_3')
            "허락을 받은 [lamb]가 기쁜 얼굴로 가판대를 향해 달려갔다."
            "곧 따끈따끈한 버터쿠키 하나를 손에 든 채 우물거리며 돌아왔다."
            T "여, 여기 쿠키는, 평소 먹던 것보다 커다래서, 신기해... 도넛 같이 생겼어."
            T "맛있다..."

            menu:
                "가만히 내버려둔다.":
                    $ belief += 2
                    $ change_image('smail_2')
                    "나른한 얼굴로 쿠키를 씹던 [lamb]가 눈이 마주치자 뺨을 붉히며 방긋 웃었다."

                "너만 먹어?":
                    $ belief -= 2
                    $ change_image('base_3')
                    T "어, 어어...? [you]도 배, 배고팠어?"
                    T "아, 그, 어쩌지, 이, 이거라도, 먹을래?"

                    menu:
                        "당황한 [lamb]가 내민 버터쿠키를 먹을 거야?{fast}"
                        "[lamb]의 쿠키를 손에 들고 먹는다.":
                            $ belief += 2
                            $ change_image('smail_1')
                            T "맛, 맛있지... 버터쿠키, 좋아해?"
                            T "아무튼 음, 둘이 나눠먹기엔 조금, 모자를지도 모르지만... 가, 같이 먹으니까 좋다."
                            T "우리가 친구처럼 느껴져."

                        "아~":
                            $ belief += 2
                            $ change_image('what')
                            T "엣, 어...?! 그, 으으음..."
                            T "아, 아아...?"
                            "같이 입을 벌리며 내민 쿠키가 [you]의 입에 닿았다!"
                            $ change_image('smail_2')
                            T "히, 히히. 재밌다. 나, 나는 왜 입을 벌렸지. 창피해."
                            
                        "됐어.":
                            $ belief -= 2
                            $ change_image('base_2')
                            T "미, 미안, 다음, 다음엔 네 것도 사 올게. 네가 이걸 워, 원하는 게 맞나...?"
                            T "어렵, 다... 정말 미안해. 나, 나는, 바보여서... 말해주지 않, 으면 몰라..."

        "나도 사 줘.":
            $ belief += 2
            T "어... 그래. 너무, 비싼 건 힘들지만... 뭐, 먹고 싶어?"
            T "지금은 추워서 아, 아이스크림은, 안 파나 봐."
            T "으, 음. 그냥 같이, 가서 볼까...?"

            menu:
                "기다릴게.":
                    $ belief -= 1
                    T "아, 응. 일단 뭐 먹고 싶은지... 으응, 그냥 내가, 사, 사 올게. 잠깐만."
                    # 화면밖 나가는 효과를... 넣을까
                    $ change_image('panic_6')
                    hide panic_6 with moveoutleft
                    "후다닥 [lamb]가 달려갔다. 꽤 빠르다!\n도망치는 일이 잦아 달리는 건 자신 있다."
                    "얼마 지나지 않아 헥헥거리며 돌아온 [lamb]가 손에 든 걸 내밀었다."
                    # 들어오는 효과
                    show panic_6 with moveinleft
                    hide panic_6
                    $ change_image('tough_1')
                    T "헉... 으아... 그러, 니까, 잠시만... 헥... 후우..."
                    T "버터쿠키랑, 초콜릿이랑, 츄러스랑, 호, 호두 파이 조각 하나... 어, 너무 많은가...?"
                    $ change_image('smail_7')
                    T "그, 그래도 {을를}[you]{/을를} 좋아하는 만큼, 사 온 거니까... 헤헤."
                    T "선물이야. 같이 와 줘서, 정말 고마워."

                    menu:
                        "... 나도 고마워.":
                            $ belief += 3
                            "눈을 마주한 [lamb]가 활짝 웃었다."
                            "둘은 든든하게 배를 채웠다."

                        "당연히 감사해야지.":
                            $ belief += 5
                            T "마, 맞아. 신 님도 늘 감사, 하랬어."
                            T "{은는}[you]{/은는} 특히 나, 나의 신앙을, 위해서 어울려주는, 거니까..."
                            T "아무리 감사를 전해도, 부족해."
                            "대부분의 간식은 [you]에게 할당됐다."
                            "[lamb]는 모르겠으나 적어도 {은는}[you]{/은는} 든든하게 배를 채웠다."

                        "싫어하는 것들만 있어.":
                            $ belief -= 4
                            $ change_image('warry')
                            T "... 아, 그, 그래...?"
                            T "음, 음... 역시 가, 같이 가서, 골랐어야 했, 했을까."
                            T "미안... 잘 하는 게 하나, 도 없네..."
                            T "이건 나 호, 혼자 먹을게... 미안해."
                            "울상을 지은 [lamb]가 안쓰럽게 간식들을 입에 넣는다."
                            "양이 많아서 시간이 걸렸다. 눈치 보며 최대한 빨리 우물거리지만, 쉽지 않은 듯하다."
                            "둘은 거리에서 꽤 긴 시간 추위에 떨었다."

                "그래. 가 보자.":
                    $ belief += 3
                    scene outBg6 with dissolve

                    "둘은 가판대 앞에 섰다. 옆에 어린 아이들이 간식을 보채는 모습이 눈에 들어온다."
                    $ change_image('base_2')
                    T "음... 나, 는... 버터쿠키 먹을래. {은는}[you]{/은는}?"
                    menu:
                        you "나는......"
                        "너랑 같은 거.":
                            $ belief += 3
                            T "아, [you]도 버터쿠키, 좋아해?"
                            T "사실은 있지. 난 감각이 마, 많이 둔해서... 맛도 잘 못 느끼거든.\n아...! 아예 망가, 진 건 절대 아니니까...!"
                            $ change_image('smail_1')
                            T "아무, 아무튼 그래서, 가끔 슬픈데... 버터쿠키는, 부드러워서 좋아해."
                            T "맛은 희미해도 식감이 조, 좋으니까."
                            T "... 갑자기 이상, 한 이야기를 했네. 미안."
                            "[lamb]가 버터쿠키 두 개를 주문하고 하나를 네게 내밀었다."
                            "한 입 먹으면... 부드럽다."
                            $ change_image('smail_2')
                            T "히히, 같이 먹으니까, 좋다. 저, 정말로 노는 것 같아."
                            T "다음에도 이런 날이 있으면 좋겠어."

                        "적당히 고른다.":
                            $ belief += 2
                            $ change_image('base_3')
                            T "음, 음... 나도 하, 한 입만 주면 안 돼...?"
                            menu:
                                "싫어.":
                                    $ belief -= 3
                                    $ change_image('base_2')
                                    T "앗... 그, 그래..." 

                                "[lamb]의 입가에 간식을 내민다.":
                                    $ belief += 3
                                    T "어... 음..."
                                    "간식을 잡을 듯 말 듯 손을 움직이다 어설프게 고개만 빼어 한 입 먹었다."
                                    $ change_image('smail_2')
                                    T "우음... 마, 맛있다. 헤헤..."

                        "풍선.":
                            $ belief += 1
                            $ change_image('base_3')
                            T "응? 풍선? 상관은 없, 는데..."
                            T "[you], 풍선도 먹을 수 있어...?"
                            $ change_image('warry_2')
                            T "아, 아니...! 아닌 거 나도 알고 있으니까...!"
                            T "그렇, 게 이상하게 보지 말아, 줘."
                            $ change_image('base_2')
                            T "아... 음... 무슨 색으로, 살 거야?"
                            T "분홍색은 내 머리가 떠다니는 것 같아서 싫어..."
                            menu:
                                "분홍색":
                                    $ belief -= 4
                                    T "엣, 저, 정말로...? {color=#2E2E2E}\n방금 싫다고, 말했는데...{/color}"
                                    $ change_image('panic_2')
                                    T "참아, 볼게. 으으..."
                                    "불편한 얼굴로 돈을 지불한 [lamb]가 [you]의 손에 분홍색 풍선을 쥐여줬다."
                                    "풍선을 보지 않으려 고개를 숙인 몸이 작게 떨린다."
                                    T "미, 미안. 아무것도 아냐, 아냐. 난 아, 아무것도 보지 않아, 않았어. 난 괜찮아."

                                    menu:
                                        "뭘 봤어?":
                                            $ belief += 6
                                            $ change_image('scary_1')
                                            T "뭐, 뭘 봤냐고? 별로 말하, 고 싶지 않아..."
                                            T "아, [you], 나 너무 두려워..."
                                            T "{i}형제{/i}가 네, 네 줄에 매달, 려 있어. 풍선이 아니라. 나, 날 내려다보고 있어."
                                            T "눈이 마주칠 거야. 싫어, 끔찍해..."
                                            T "살려주세요, 살려주세요. 아, 어머니......"

                                        "손을 잡아준다.":
                                            $ change_image('sad_1')
                                            $ belief += 3
                                            "흠칫 놀란 [lamb]가 이내 안정을 구하듯 네게 더욱 붙었다."
                                            T "미안, 미안해. 나 무서워. [you], 안아줘. 무섭, 고 싶지 아, 않은데에..."

                                        "무시한다.":
                                            $ belief -= 3
                                    # 못생겻다
                                    $ change_image('scary_2')
                                    show expression AlphaMask("black70", At(new, center)) as mask at multiply
                                    "결국 울음을 터뜨린 [lamb]가 바닥에 주저앉아 몸을 웅크렸다."
                                    
                                    play sound "audio/sound/웅성웅성.mp3"

                                    "주변에서 우리를 이상하게 보는 시선이 느껴진다."

                                    menu:
                                        "일단 다른 곳으로 데려간다.":
                                            $ belief += 3

                                        "눈을 맞추며 위로한다.":
                                            $ belief += 5

                                        "정신 차려. 지금 뭐하는 거야?":
                                            $ belief -= 5
                                    
                                    "순간, 갑자기 불어온 강한 돌풍에 풍선이 날아갔고{w}\n곧 나무 가시에 찔린 풍선은 작은 비명을 지르며 허무하게 터졌다."
                                    hide mask
                                    $ change_image('sad_1')
                                    "[lamb]가 그것을 올곧게 응시했다."

                                    $ change_image('sad_8')
                                    T "... 하하... 내, 내 머리가, 터졌네."
                                    T "아, 아냐... 이게 아니, 지... 뭐, 뭐였더라..."
                                    T "...... 소란, 피워서 미안해... 아. 근데 왜 저건 아직도..."
                                    menu:
                                        "귀를 기울인다.":
                                            $ belief -= 10
                                            T "{i}... 왜 아직도 눈을 뜬 채 죽어있지?{/i}"
                                            
                                        "눈을 가려준다.":
                                            $ belief += 4
                                            T "거, 걱정해주는, 거야? 신경, 쓰게 해버렸구나. 미안해..."

                                        "내버려둔다.":
                                            $ belief -= 5
                                            "[lamb]가 무어라 중얼거렸지만 잘 들리지 않았다."

                                    $ change_image('black_2')
                                    "불규칙적인 속도로 연신 깜빡이는 눈꺼풀이 보인다."
                                    T "으, 이제 일어나야지... 부끄러운, 모습을 보였어... 정말 미안해..."
                                    T "... ..."
                                    $ change_image('base_2')

                                "너는?":
                                    $ belief += 3
                                    $ change_image('smail_1')
                                    T "... 나? 으음, 난 고른다면... 검은색, 이려나. 여기 검은 풍선은 어, 없지만. 히히."
                                    T "어머니의 색이거든. 그래서 꽤, 좋아해. 어둠도 무서, 무서운데. 밝은 것보다 괜찮고..."
                                    T "무슨 색을 할지 모르겠다면, 노란색은 어때? 그냥... 화사하다고, 다들 그러니까."

                                    menu:
                                        "노란색은 네게도 있네.":
                                            $ belief += 3
                                            $ change_image('smail_5')
                                            T "아, 으응, 노란색은 우, 우리 어머니에게 받았어. 계속 말하니까 어, 어머니를 엄청, 좋아하는 것 같네. 헤헤..."
                                            T "마, 맞으니까... 상관 없나."
                                            T "우리 집안은 신기하게, 다들 분홍색 머리야. 유전자가 강한 걸까? 아버지도 분홍색 머리셔. 할아버지도, 그 위 할머니도..."
                                            T "아마 내 후대도, 분홍색 머리가 되지 않을까. ... 볼 일은 없겠지만."
                                        
                                        "다른 색을 말해 줘.":
                                            $ change_image('base_3')
                                            T "아, 아니면, 하얀색이라던가? 눈이랑, 구분이 안 되려나..."
                                            T "그치, 만 하얀색도, 예쁘잖아. 나, 나는 하얀색만 보면, 파고들고 싶어져."
                                            T "눈에 파고들기엔 옷도 더러워지고 추워서 혼나지만... ..."
                                            $ change_image('smail_2')
                                            T "전부 하얗게 물들어서 아무것도 보이지 않았으면 좋겠어."


                                "원하는 색을 말해준다.":
                                    $ belief += 2
                                    $ change_image('base_2')
                                    "고개를 끄덕이며 {이가}[you]{/이가} 말한 색의 풍선을 구매해 손에 쥐여줬다."
                                    "[lamb]가 빤히 {을를}[you]{/을를} 바라본다."

                                    menu:
                                        "... 왜?":
                                            $ belief += 3
                                            T "... 그냥, 기억하고 싶어서."
                                            T "조, 좀 부담스러, 웠으려나. 미안."
                                            T "널 눈에 담고 싶었어."

                                        "뭘 봐.":
                                            $ belief -= 3
                                            T "아, 어, 미안, 안 볼게..."
                                            "눈을 깐 [lamb]가 스을쩍 [you]의 소매를 잡았다."
                                            
    T "... 슬슬, 돌아갈 시간이야."
    T "해, 해가 지기 전에, 가야 해."
    T "가자, [you]."

    scene outBg5 with dissolve
    play sound "audio/sound/눈을밟음.mp3"

    "앙상한 나뭇가지 아래를 지나가면 눈이 밟히는 소리가 선명히 귓가에 울린다."
    "[lamb]는 몇 번이고 뒤를 살피고 발자국을 다시 밟는 장난을 치며 {을를}[you]{/을를} 따라갔다."

    menu:
        "얌전히 있어.":
            $ belief -= 2
            $ change_image('what')
            T "앗, 미, 미안. 미안해. 얌전히 걸을게..."

        "기분이 좋은가 보네.":
            $ belief += 5
            $ change_image('smail_2')
            T "헤헤. 으응, 오늘은 즈, 즐거운 날이니까. 좀 힘든, 일도 있었지만..."
            T "익숙하니까, 괜찮아."
            T "다 잊어버렸어. 잊어야 해."

        "뒤에 뭐가 있어?":
            $ belief -= 4
            $ change_image('say_2')
            T "뒤. ... 아... 뒤에, 아무것도 없어."
            T "어차피 또 나만 보일, 거야. 괜찮아. {i}형제{/i}는 멀리 있어."
            T "나, 나도 익숙해질 수, 있어."

    "곧 산만한 행동을 멈추고 얌전히 옆을 걷는다."
    $ change_image('base_2')
    T "... 있지. 혀, 형제는 왜 늘, 나를 따라올까?"
    T "어, 어려운 질문인 거, 나도 알아. 그렇지만 나는 늘..."
    T "왜 이런, 일이 생겼는지. 항상 궁금하고 원망스러워."
    T "다른, 사람들은 괜찮은데, 왜 나만... ..."
    T "종교는 언제나 내, 내가 잘못했대. 전부 신 님을 따르, 지 않아서..."
    T "그래, 그래서 나도 엄청, 노력하는데... 그분에겐 하, 항상 부족한가 봐."
    T "이런 이야기도 죄겠지? 너, 너도 벌을 받으면, 안 될 텐데."

    menu:
        "알면 더는 의문을 품지 마.":
            $ belief += 2
            $ change_image('say_2')
            T "그, 그치마안... 아까 본 연극, 처럼 어른들은 항상, 따르기만 하래."
            T "나는, 바보여서 이해하지 못한, 못한 건 외우지도, 못한단 말이야."
            T "왜 물어보는 것도, 죄가 되는 거야?"
            T "이, 이것도... 그분이랑 가까워지려는, 행동이잖아."
            T "... 가까워지려는 게, 주제넘은, 걸까."
            T "적당한 거리를 모, 모르겠어. 일단 난 다가가려고, 애써야 된다지만..."

        "그분은 그것도 품어주실 거야.":
            $ belief += 7
            $ change_image('warry')
            T "정말? 정말 그러실까?"
            T "그, 그랬으면, 좋겠다. 괜찮다고, 말해주시면 좋겠어."
            T "근데 나는 요, 욕심이 많나 봐. 잘했다고 칭찬, 도 해주시길, 원해."
            T "... 그분이랑, 직접 만날 수 있다면 좋을 텐데."
            T "눈 앞에 없, 으니까 잘 못 느끼겠어."
            T "어떤 사람은, 자신의 신을 봤대. 나도 더 노력하면, 볼 수 있을까."

            menu:
                "진짜 만나게 되면 뭘 하고 싶어?":
                    $ belief += 5
                    $ change_image('eye_bottom')
                    T "만나게 되면... 괜찮다고, 걱정 말라고 말해달라 부탁하고 싶어..."
                    T "나는 앞으로 두려움도 모를 거라고. 더는 무섭지 않을 거라고."
                    T "시, 신 님이 뱉은 말은, 전부 사실이 되잖아."
                    $ change_image('say_2')
                    T "그럼 분명 나도, 구원받을 거야."

        "종교에 의존하지 말고 스스로 생각해.":
            $ belief -= 5
            $ change_image('warry')
            T "스, 스스로... 안돼, 난 못해..."
            T "[you]처럼 호, 혼자서 뭐든, 할 수 있는 사람이 아냐."
            T "이겨낼 수 있었다면 진작에 이겨냈어."
            $ change_image('angry_2')
            T "네, 네가 뭔데 함부로 말해...?!"
            $ change_image('warry')
            T "으... 미안, 또 흥분했네... 아, 아아, 무튼, 그분을 모욕하면, [you]라도 무사하지 못, 할 거야."
            T "혼나면 무서워, 혼나면 아파..."

    # 마우스 좌클릭 불허 설정
    $ use_click(False)
    $ change_image('base_2')
    pause(2)
    $ threading.Timer(0.1, use_click, [True]).start()
    
    # 확인 필요
    $ change_image('boom_1')
    T "흐아악...!"
    $ change_image('panic_6')
    hide panic_6 with moveoutleft
    "중얼이던 [lamb]가 곧 무언가를 보고 작은 비명을 지르며 [you]에게 달라붙었다."

    menu:
        "떨어져.":
            $ belief -= 3
        "왜 그래?":
            $ belief += 5
        "내가 그렇게 좋아?":
            $ belief -= 1

    scene outBg8 with dissolve

    T "저, 저기, 사라, 사람이..."
    T "... 아... 그, 그냥, 취객이었네..."
    T "미, 미안... 형제가 가까이 온 줄 알고... 모르, 는 아저씨야."
    T "죽은, 건 아닌 것 같은데, 이, 이대로 두면, 죽으려나."
    T "[you], 어떡하지. 도와, 주는 게 나을까. 난 잘 모르겠어..."

    menu:
        "어떡할까?{fast}"
        "선을 행하라고 늘 배웠잖아.":
            $ belief += 10
            $ change_image('base_2')
            T "그으... 렇지."
            T "어, 어떻게 해야 하지? 이런 적은 처음, 처음이라."
            T "바, 방법을 알려주면, 안 될까?"
            menu:
                "스스로 생각해, [lamb].":
                    $ belief -= 5
                    $ change_image('what')
                    T "안, 안 돼. 정말 모, 몰라서 묻는 거란, 말이야."
                    T "스, 스스로라니, 잘못되면 책임지기 싫, 어..."
                    T "아... 이럴 때는 으으... 겨, 경찰을 부르면, 되려나...?"
                    T "어, 음, 이, 일단 깨우고, 그래."

                "깨우고 실내로 데려가.":
                    $ belief += 5
                    $ change_image('say_2')
                    T "그렇구, 나. 그, 그러면 돼..."
                    T "고마워 [you]. 나 혼자서는 부, 분명 아무것도, 못 했을 거야."

            T "그, 근데 내가 깨워...?"
            T "아, 아아, 알았어. 하, 할게. 아저, 아저씨...? 일어나세요... 제발..."
            "간절한 목소리에도 취객은 죽은 듯 미동이 없었다."
            $ change_image('sad_5')
            "울 것 같은 얼굴로 취객을 흔들던 [lamb]가 도움을 요청하듯 {을를}[you]{/을를} 바라본다."
            T "아직 사, 살아있는 것 같은데, 어, 엄청 차가워. 어떡하지. 모르겠어."

            menu:
                "지금 죽이는 게 그를 구원할 방법일지도 몰라.":
                    $ belief -= 7
                    $ change_image('what')
                    T "뭐, 뭐...? 무, 무슨 소리야. 우린 이 사람을 사, 살릴려고, 이러는 거잖아."
                    T "죽길 바라, 바라면 그냥, 버리고 가면 되는걸."
                    T "지금 농담할, 기분 아니야. 나, 나 좀 도와줘!"
                    $ change_image('base_3')
                    "그때, [lamb]의 외침 덕분인지 취객이 옅은 신음을 흘리며 눈을 떴다."

                "뺨을 때려 봐.":
                    $ belief -= 3
                    T "때, 때려...? 그래도 되, 는 거야?"
                    T "모, 못하겠는데... 아... 으..."
                    $ change_image('panic_6')
                    # 흔들리기이
                    "{i}퍼억!{/i} 결국 눈 꼭 감은 채 주먹을 치켜든 [lamb]가 취객의 이마를 내려찍었다!"
                    "분명 뺨을 때리라고 했는데... 왜 이마를 때린 걸까?"
                    $ change_image('panic_2')
                    "자신이 때려놓고 아픈 건지 인상을 가득 찌푸리며 손을 감싸쥐었지만, 그래도 큰 고통에 취객이 눈을 뜰 수 있었다."

                "같이 취객을 깨운다.":
                    $ belief += 5
                    "[you]의 목소리에 금방 취객이 눈을 떴다."
                    $ change_image('what')
                    T "대, 대단해 [you]...! 아, 어떻게 한 거야? 왜 내가 깨울 땐, 반응도 없었지...?"

            "눈을 뜬 취객이 뭐라 웅얼거리는 소리가 들렸지만 알아들을 수 없었다."
            "눈은 떴으나 몸은 제대로 가누지 못하는 듯싶다."

            $ change_image('say_2')
            T "어디든 따뜻, 한 곳으로 데려가야... 하는데..."
            T "마침 저기에 파출소, 가 있어."
            T "내가 이쪽을 부, 부축할 테니까, {이가}[you]{/이가} 반대쪽, 잡아줄래...?"

            menu:
                "그래.":
                    $ belief += 8
                    T "고마워... 고맙, 다고 하는 게 맞는지, 모르겠지만..."

                    scene outBg10 with dissolve

                    "둘은 열심히, 협동해서 취객을 부축해 파출소까지 데려갔다."
                    $ change_image('base_2')
                    T "아, 음. 난 별로, 여기 들어가고 싶지, 않아."
                    "입구에서 우뚝 서서 멈춘 [lamb]가 곧 힘을 빼며 취객을 놓아버렸다."
                    "갑자기 늘어난 무게가 {을를}[you]{/을를} 짓누른다."

                    menu:
                        "너는..."
                        "같이 취객을 놓아버린다.":
                            $ belief -= 2
                            "쿵. 인간이 힘없이 쓰러지는 소리가 울렸다."
                            T "... 이, 이래도 괜찮겠지. 안에 어른들도 있으니까."

                        "뭐하는 거야. 똑바로 안 해?":
                            $ belief -= 5
                            $ change_image('scary_1')
                            T "그치마안... 여, 여기 무서워. 아버지가 여긴, 아주 잘못한 사람만 간댔어."
                            T "나는 죄, 죄인이랬으니까, 들어가면 분명, 잡혀갈 거야."
                            menu:
                                "어린 애처럼 굴지 말고 정신 차려.":
                                    $ belief += 2
                                    $ change_image('panic_2')
                                    T "으, 알았, 알았어. 할게..."
                                    "파출소 내부까지 취객을 인도한 뒤 어른들의 칭찬을 받고 나서야 빠져나올 수 있었다."

                                "같이 취객을 놓아버린다.":
                                    $ belief -= 2
                                    "쿵. 인간이 힘없이 쓰러지는 소리가 울렸다."
                                    T "... 이, 이래도 괜찮겠지. 안에 어른들도 있으니까."

                        "[lamb]. 끝까지 해야지.":
                            $ belief += 4
                            $ change_image('scary_1')
                            T "그, 그치만 무서워... 병원, 이랑 닮아서..."
                            T "... ... 아냐. 끝, 까지 하는 게, 좋겠지... 미안해. 다, 다시 들게."
                            $ change_image('panic_2')
                            T "끄응... 왜 술을, 먹으면 이렇게 무거워지는 거야?"
                            "파출소 내부까지 취객을 인도하자 등장한 어른들이 둘을 잔뜩 칭찬해주었다."
                            $ change_image('tension_1')
                            "[lamb]는 아직도 뭐가 두려운지 [you]의 뒤에 숨어버렸지만... 곧 둘은 밖으로 나왔다."

                "너 혼자 해.":
                    $ belief -= 5
                    hide say_2
                    show what_2 with hpunch
                    # 뭔가 고칠 필요가 있을지도...
                    T "어. 나, 나나, 나 혼자...?!" 
                    T "호, 혼자, 혼자는 안돼...! 이 아저씨 엄청, 무, 무겁고, 아, 그... 무겁단 말야...!"
                    T "도와줘, [you]. 혼자는 못, 못해. 무, 무서워. 싫어..."
                    hide what_2 with Dissolve(0.2)
                    # 불편쓰

                    menu:
                        "어리광 부리지 말고 빨리 갔다 와.":
                            $ belief -= 5
                            $ change_image('scary_1')
                            T "으으......"                            

                        "잘 다녀오면 상을 줄게.":
                            $ belief += 5
                            $ change_image('base_2')
                            T "상...? 뭐, 뭔데?"
                            T "아직 안 다녀와서, 안 알려주는 거야...?"
                            T "알겠, 알겠어. 갔다 올게."
                            $ sang = True

                        "할 수 있어.":
                            $ belief += 3
                            $ change_image('panic_2')
                            T "하, 할 수 있... 아, 알겠어. 갔다 오면, 되잖아..."

                    T "갔다 오는 동안 어, 어디 가버리면, 안돼... 알았지...?"
                    T "꼬, 꼭 거기서 기다, 기다려야 돼...!"

                    scene outBg10 with dissolve

                    "단호한 목소리로 말한 [lamb]가 후들거리는 다리로 어떻게든 취객을 파출소까지 부축했다."
                    "길을 걸어가면서 몇 번이고 뒤를 돌아 봐 {을를}[you]{/을를} 확인하는 모습이 우습다."
                    "도착하고서도 무엇을 해야할지 갈피를 못 잡더니 입구에 취객을 던져놓고 빠르게 [you]에게 달려간다."
                    # 뭔가 얼굴이 맘에 안듬
                    $ change_image('smail_3')
                    "뒷처리가 이상했지만 얼굴은 개운해보였다."
                    "[lamb]의 뒤에서 어른들이 밖으로 나와 취객을 안으로 데려갔다."

                    T "[you]! 나, 나 잘 하고, 왔어...! 헥... 흐악... 아 잠깐, 헉... 후..."
                    T "잘 한 거, 맞겠지...? 으응, 아무튼, 잘 된 거니까... 아마도..."

                    if sang == True:
                        T "상은 뭐, 야...? 기대, 하고 있는데..."
                        "드물게 반짝이는 노란 눈이 [you]에게 꽂혔다."

                        menu:
                            you "상은......"
                            "칭찬. 잘했어.":
                                $ belief += 5
                                $ change_image('smail_2')
                                T "나, 잘했어? 히히. 기쁘다..."
                                T "앞으로도 열심히 할게..."

                            "축복을 받았을 거야.":
                                $ belief += 2
                                $ change_image('smail_1')
                                T "어... 축복은, 신 님이 주는 거잖아."
                                T "아, 아니, 싫다는 뜻은 아니, 라..."
                                T "... 아냐. 만족해... 좋은 일을, 했으니까..."

                            "머리를 쓰다듬어준다.":
                                $ belief += 5
                                $ change_image('smail_2')
                                T "헤헤. 이러니까 왠지, 어린 애가 된 것 같네..."
                                $ change_image('hot')
                                T "더... 해 줘."
            
            "멍 때리는 와중 까마귀떼의 울음소리가 하늘을 덮었다. 저녁이 온다."
            $ change_image('say_2')
            T "아. 버, 벌써 시간이 엄청 늦었어. 빨리, 돌아가야 돼. 더 늦으면 혼날 거야..."

        "우리 알 바 아냐. 가자.":
            $ belief -= 5
            $ change_image('say_2')
            T "그래... 우리가 도와주는, 것보단 다른 어른들이 도와, 주는 게 더 낫겠지..."
            T "근처에 다른 사람은 어, 없어 보이는데."
            T "으, 음. 그, 그래도 누가 곧 오, 올 거야. 그치...?"
            T "기도를, 해주면 신 님이 도와주실지도, 몰라."
            $ change_image('pray_1')
            "취객의 앞에 서서 두 손을 모은 채 눈을 꾹 감고 기도하는 [lamb]를 보았다."

            menu:
                "바보 같아.":
                    $ belief -= 3
                    $ change_image('what')
                    T "뭐? 아, 아냐. 기도는 정말 이뤄, 진다고 교리에도 써 있어."
                    T "간절할수록 신 님이 이, 이뤄주신다고, 그랬단 말야."
                    T "함부로 그렇게 말하면 크, 큰일 나. 들키면 벌을 받을 거야."

                "잘했네.":
                    $ belief += 7
                    $ change_image('smail_2')
                    T "히... 이, 이렇게 칭찬 받으니까, 좋다..."
                    T "{은는}[you]{/은는} 기도 아, 안 해?"

                    menu:
                        "안 해도 괜찮아.":
                            $ belief -= 3
                            $ change_image('warry')
                            T "그... 그래? 그렇구나... 나는, 했는데... 어..."

                        "했어.":
                            $ belief -= 1
                            $ change_image('warry')
                            T "어, 언제 한 거야? 못 봤는데... 내, 내가 기도할 때 같이 한 거야?"
                            T "으, 음. {이가}[you]{/이가} 거짓말을 할 리는, 없으니까."

                        "나도 해야지.":
                            $ belief += 5
                            show expression AlphaMask("black70", At(new, center)) as mask at multiply
                            "[you]도 손을 모아 눈을 감았다."
                            "무엇을 기도했어?"
                            "[lamb]의 시선이 느껴졌다."
                            menu:
                                "훔쳐 보면 좋아?":
                                    $ belief -= 2
                                    $ change_image('warry')
                                    T "앗, 그, 아, 미안... 나도 모르게 그, 그만..."
                                    $ change_image('eye_bottom')
                                    if lamb == "아그넬로":
                                        "어린양이 눈을 내리깔았다..."
                                    else:
                                        "[lamb]가 눈을 내리깔았다..."

                                "적당히 봐.":
                                    $ belief -= 2
                                    $ change_image('warry')
                                    T "아, 아, 내가 너무, 빤히 쳐다봤구나... 미안해."
                                    T "{이가}[you]{/이가} 예뻐서, 쳐다봤어. 아마도. 응..."

                                "내버려둔다.":
                                    $ belief += 3
                                    $ change_image('smail_3')
                                    "눈을 뜨면 [lamb]가 방긋 웃으며 입을 열었다."
                    
            T "시간이, 많이 늦었네. 이, 이만 가자. 더 어두워지면, 어른들한테 혼날 거야."

    scene outBg5 with dissolve
    play sound "audio/sound/눈을밟음.mp3"
    $ change_image('base_2')

    "... ..."
    "눈이 밟히는 소리만이 공간을 채웠다. [lamb]의 목소리가 그 틈새에 스며든다."
    T "... 오늘 있었던 일들이, 신 님의 눈에 들어갈까?"
    T "신 님은, 뭐든 볼 수 있다고, 그러셨지만."
    T "호, 혹시 벌을 받을까 봐, 무서워."
    T "잘한, 일이 있어도 그건 당연한 거잖아. 시, 신도는 당연히, 그래야 해."
    T "그런데 죄는... 너무 흔해서, 아무리 주의해도, 어쩔 수 없이 저질러 버려."
    T "파고들면 죄는 어떻게든 나와."
    T "교리에선 우리가 죄인이라 그럴 수밖에 없다고 그러지만, 그걸 이, 이해해주시진, 않잖아."
    $ change_image('say_2')
    T "... 이걸 말하는 난 지금 죄를 짓고 있는 걸까?"
    $ change_image('base_2')
    "... ..."
    $ change_image('say_2')
    "... ... 아."
    T "도, 도착했네. 이제 들어, 가야겠다."

    menu:
        "그래.":
            $ belief += 1

        "잠깐만.":
            $ belief += 2
            $ change_image('base_3')
            T "응...? 뭐, 뭔가 빼먹은 거라도, 있어?"

            menu:
                "조심히 가.":
                    $ belief += 2
                    $ change_image('smail_1')
                    T "...? 하하. 그, 그래. [you]도 아, 안녕, 아무것도 들키지 말고 들어가."

                "좋은 점도 찾으면 나와.":
                    $ belief += 2
                    $ change_image('base_3')
                    T "... 그, 그래? 나도 있, 는 거야? 예를 들면 어떤, 어떻게...?"

                    menu:
                        "예를 들면...{fast}"
                        "귀여워.":
                            $ change_image('base_2')
                            T "귀여... 그... 런 게 쓸모가, 있어?"
                            T "그, 그분이 귀여운, 걸 좋아하실 것... 같진 않은데..."
                            $ change_image('ang')
                            T "[you] 눈에만 귀여우면, 어떡해... ..."

                        "착해.":
                            $ change_image('smail_1')
                            T "차, 착한가... 그, 그래도 착하면, 그분이 좋아하시니까... 기뻐."
                            T "사실 착하다는, 개념이 정확히 어떤 건진 모, 모르겠어. 그치만 여기선 잠잠히 순, 종하는 걸, 착하다고 말하는... 거겠지?"
                            $ change_image('tension_1')
                            T "... 내, 내가 순종을, 했던가... ... 아..."

                        "기특해.":
                            $ change_image('base_2')
                            T "...? 정확히 뭐가...?"
                            T "아, 거, 거기까진, 안 말해주는... 거야? 그래... 포괄적으로, 말, 한 거구나..."
                            T "그렇지...?"

                        "직접 말한다.":
                            "당신이 직접 말한다 해도, [lamb]는 이해하지 못한다. [lamb]의 행동은 고정되어 있으니까."
                            "그럼에도, 닿지 않을 진심을 전하겠다면."
                            $ good = renpy.input('말해 줘.', length=32)
                            "{i}[good]{/i}"
                            "당연하게도 [lamb]는 이해하지 못했다. 네가 좋은 점이 아닌 욕설을 말했다 해도 슬퍼하지 않고, 진심어린 칭찬을 했다 해도 감동하지 않는다."
                            "하지만... 문장을 써내려간 정성에는 감사하고 있어."
                            $ change_image('smail_7')
                            "[lamb]는 조용히 웃었다."
                    
                    $ change_image('smail_7')
                    T "헤헤... 자, 잘은 모르겠지만, 기뻐. 고마워."
                    T "나도 언젠가, {을를}[you]{/을를} 가득 창, 찬할 수 있으면, 좋겠다."

    T "오늘, 저, 정말로 즐거웠어. 여러 일이 있었지만... 기회가 된다면 다음에도, 놀고 싶어."
    T "[you], 조심히 들어가. 꾸, 꿈에서도, 놀면 좋겠다."
    T "이만 갈게. 좋은 밤 돼. 나중에, 또 보자."

    hide hide_image
    scene black
    with dissolve

    $ day += 1
    jump room