image eatBg1 = "images/backgrounds/eatBg/eat-1.png"
image eatBg2 = "images/backgrounds/eatBg/eat-2.png"

# 6 식사
label eat:
    $ eatX = 1
    scene eatBg1 with dissolve

    $ change_image('base_2')
    T "이, 이게 무슨 행위인지, {은는}[you]{/은는} 알아...?"
    menu:
        "알아.":
            $ belief += 3
            T "그렇구나... 나는 늘, 헷갈렸거든."
            T "어떤, 교리에는... 식사란 숭고한 행위라고 하던데..."
            T "나, 난, 이게 의식, 과 뭐가 다른지 잘 모르겠어."
        "몰라.":
            T "[you]도 모르는 거야...? 그, 그럼 내가, 설명해 줘야, 겠네."
            $ change_image('smail_1')
            T "이러니까, 선생님이 된 것 같아. 헤헤."
            T "'식사'라는 행위는, 신이 우리에게 준, 삶의 축복인 행위래."
            T "먹는 건 다른 것의 생명을 빼앗아서, 자신과 하나 된 뒤에 스스로의 생명을 이어나가는, 거니까."
            T "먹는 것은 특히, 트, 특별하고, 고결해야 한다고... 교리서에서, 봤어."
    T "아무튼 지금은... 식사, 시간이니까..."
    T "오늘은 특별한, 음식을 먹는대."
    T "식당엔 우리밖에 없는 것 같지만... 걱정 마. 이, 이런 날처럼, 어른이 없어도, 먹을 수 있도록, 준비되어 있으니까..."

    call eat_food from _call_eat_food

    $ change_image('smail_1')
    T "그러고 보니까 갑자, 기 생각났는데... 그런 이야기, 들은 적 있어?"
    T "어떤 종교는, 신을 잡아 먹는대."
    T "또 어떤 종교는... 신을 잡아 먹는 것처럼, 유사한 음식을 만들어서 먹는다고, 하더라고."
    T "... 이것도 우리의 신일까?"
    $ change_image('smail_5')
    T "... 하하. 너, 너무 이상한, 말이다. 그, 치."

    menu:
        "이상하지 않아.":
            $ belief += 3
            T "그, 렇게 말해줘서, 고마워."

        "그러게. 이상해.":
            $ belief -= 2

    T "으음, 그래도, 아주 별로인... 맛은 아니어서, 다행이야."
    $ change_image('eye_right')
    T "너도... 맛없지 않았다면 먹을 수 있었을까......"
    "......"

    menu:
        "무슨 말이야?":
            $ belief -= 2
            T "응...?"
            $ change_image('what_2')
            T "아...! 아, 아아, 아무것도 아냐!" with hpunch
            T "시, 신경쓰지 마, 내가 잠깐, 어... 제정신이 아, 아니었나 봐..."
            T "미안, 미안해... 이런 말은, 음... 기분이 나쁜가... ..."
            $ change_image('tension_1')
            T "잘은, 모르겠지만... 역시 좀, 불편하겠지...?"

        "먹어 봐.":
            $ belief += 3
            $ change_image('base_2')
            T "... 머, 먹어 봐도 돼?"
            $ change_image('panic_1')
            T "... ..."
            if love:
                $ change_image('panic_2')
                T "너무 부, 불경한 생각, 같아... 경건하게 에, 엘시를 먹는다는 건... 있을 수 없어."
                T "이런, 이런 상상은 중죄야. 용서해 줘, 엘시..."
            else:
                T "어떻게... 먹어야 되지. 아, 안 아프게 먹는 방법을 모르, 모르겠어."
                T "{은는}[you]{/은는} 알... 리 없나. 보통은 안, 안 먹지."
                T "그런 거 변태도 안 하는, 거잖아."

            $ change_image('panic_2')
            T "으... 속이 메스꺼워......"

        "먹지 마.":
            $ belief -= 1
            $ change_image('base_2')
            T "... 앗, 그, 그렇지. 먹으면, 안 되지... 미안해..."
            T "저, 정말 먹으려던 건, 아니었어. 정말이야. 아마, 아마도."
            T "이런 말, 을 해서 미안해. 아, 정말로... 미안해."

    $ change_image('base_2')
    T "그치만 말야. 속의 재료를 모르면, 본래의 모습이 보, 보이지 않으면. 먹음직스러우면... 그, 그게 뭐든 우리는 먹을 수, 있지 않을까?"
    T "어, 어떤 끔찍한 재료라도. 무지하면 받아들일 수 있을 거야."
    $ change_image('smail_2')
    T "뭔가 기, 기분이 좋네... 헤헤. {은는}[you]{/은는} 이런 이야기, 어때?"

    menu:
        "기분 나빠.":
            $ change_image('base_2')
            $ belief -= 2
            T "아... 미, 미안. 아까부터 자꾸 이상한 소리만 했구나..."
            T "이제 조용히 먹, 먹을게."

        "재미없어.":
            $ change_image('base_2')
            $ belief -= 1
            T "그으... 그런가. 식사하면서 할 얘기는, 아니야?"
            T "내, 내 이야기는 늘 재미, 없지만..."
            T "입 다물게... ..."

        "좋아.":
            $ belief += 3
            $ change_image('smail_6')
            T "... ...! 정, 정말? 진짜로...?"
            T "재, 재밌게 느껴서, 다행이다. 이런 얘기는 그, 할 사람이 없으니까."
            $ change_image('smail_2')
            T "{이가}[you]{/이가} 들어줘서, 정말 기뻐. 히히."

    show expression AlphaMask("black70", At(new, center)) as mask at multiply
    "약간의 시간이 흐른다. 침묵 속에 식기가 부딪히는 소리만이 울렸다."
    "나름 깨끗하게 음식을 비우고, 입맛을 다시던 [lamb]가 손을 꼼지락거리며 [you]에게 말했다."
    hide mask
    $ change_image('smail_1')
    T "[you]... 괜찮다면 우리, 차, 차라도 마실래?"
    T "여기선 값싼, 차밖에 없지만... 그래도 나, 나쁘진 않을 거야. 어... 내 혀가 둔해서, 아니려나..."
    T "{은는}[you]{/은는} 식후에 차를 마신 적 있어?"

    menu:
        "있어.":
            T "있구나. 특별히 조, 좋아하는 차라던가 있는, 거야?"
            T "말해주면 나중에 구해, 구해볼게. 비싸도 괜찮아. 돈은 다, 다른 곳에서 받으면 되니까."
            $ change_image('very_happy')
            T "그럼 그때. 나랑 같이... 티타임을 가지자. 두, 둘이서만. [you]만 상관 없다면..."

        "없어.":
            $ change_image('base_3')
            T "없어? 트, 특이하네. 서민들도 잘 마신다고, 알고 있는데..." 
            T "이번에 가, 같이 먹어보는 건, 어때? 별로면 뱉어도 되니까..."

    $ change_image('smail_3')
    T "조금만 기다려 봐. 금방 준비, 준비해서 가져올게!"
    show expression AlphaMask("black70", At(new, center)) as mask at multiply
    "곧 둘이 먹은 그릇까지 대충 쌓아 들며 부엌 안쪽으로 [lamb]는 향한다."
    "찻잎과 주전자, 잔을 꺼내는 뒷모습이 꽤 능숙하다. 여긴 신도가 마음대로 부엌을 써도 괜찮은 건가?"
    "조금 전까지 요리가 올라가있던 쟁반에는 방금 꺼낸 준비물들이 올려진 채 식탁에 다시 안착했다."
    "주전자에선 뜨거운 김이 새어나왔다. 언제 이렇게 빨리 끓인 걸까? 마치 미리 준비한 것처럼."
    
    hide mask
    hide smail_3
    show panic_2 at ouch_sm
    # $ change_image('panic_2')
    show expression AlphaMask("black70", At("panic_2", center)) as mask at multiply
    "[lamb]는 전혀 의문스럽지 않은 건지 식은땀을 흘리며 찻잔에 차를 준비했다. 물을 따르는 손이 달달 떨리는 건 무시하자."
    "곧 [you]의 앞에 잔이 놓여졌으나 [lamb]는 마주 앉지 않은 채 오히려 뒤를 돌았다."

    menu:
        "어디 가?":
            $ change_image('smail_1')
            T "으, 응? 설탕이랑... 우, 우유를 가지러. [you]도 먹을래? 히히..."

        "무슨 차야?":
            $ change_image('smail_1')
            T "어... 그, 그냥 홍차지. 여기 고급진, 차는 없잖아. 그런 건 사치, 사치품이야."

    "부엌 구석의 문을 열자 세차게 바람이 부는 소리가 들리고 순식간에 한기가 들이닥쳤다."
    
    play sound "audio/sound/바람.mp3"

    $ change_image('panic_2')
    "움찔 떨며 나간 [lamb]는 얼마 지나지 않아 병과 작은 통을 양손에 끼고 돌아왔다. 그새 손이 잔뜩 붉어진 것이 밖이 많이 추운가보다."
    "설탕 두 스푼에 우유 네 모금. 꽤나 어른스럽지 않은 레시피다."

    hide mask
    $ change_image('smail_5')
    T "어... [you]도 우유, 넣을래? 이, 이렇게 먹으면 뜨거운 것도 식어서, 괜찮아."

    menu:
        "됐어.":
            T "이대로도 괜찮아? 설탕 넣으면, 단데..."
            T "나, 나만 유치하게 먹나 봐. 사실 우리 집에서는, 이렇게 먹는 건 절대 허락, 되지 않았거든."
            T "그래서 그냥 홍차도 마실 수 있지만... 그치만 이게 더, 특별해."
            $ change_image('smail_2')
            T "몰래 건전한 장난을 치는 것 같잖아. 헤헤, 좀 유치해도."

        "그래.":
            "[you]의 허락이 닿자마자 찻잔 안으로 우유가 쪼르르 담겼다. 흰 색이 찻물에 서서히 섞인다."
            $ change_image('smail_2')
            T "설탕, 은 여기 있으니까. 취향대로 넣어. 헤헤. 가, 같이 같은 걸 먹으니까, 좋다."

        "무슨 우유야?":
            $ change_image('base_2')
            T "어... 글쎄? 우유 맛은 잘, 못 구분해서... 그냥 평범한 우유, 아닐까?"
            T "우유는 마, 맞아. 전에 어른이 이걸로 치즈를 만드는, 걸 본 적 있어. 아마도."
            T "음... 염소 젖인가...? 모르겠다."

    $ change_image('smail_9')
    "아무튼. 둘은 평화롭게 차를 마셨다. 날은 고요하고 속은 따뜻하다. 지독히 평화롭고 평온한..."
    "늘 이런 순간만이 있기를 바라는가?"

    hide mask
    $ change_image('eye_right_2')
    T "있잖아."
    T "사실 여기 있는 음식은, 다 어른들 거야. 이, 일반 신도에겐 허락되지 않았어."

    menu:
        "근데 왜 먹었어?":
            $ change_image('base_2')
            T "맛있, 맛있잖아. [you]에게도 맛보여주고, 싶었어. 자, 잘 먹었는진, 모르겠지만..."
            T "일반 신도의 밥도 먹을 만 하, 지만 이쪽이 더 특별하니까."

        "어쩐지 맛있더라.":
            $ change_image('smail_2')
            T "히히 그치. 같이 먹길 자, 잘했어?"
            T "잘 했다고 해 줘 [you]... 네가 기뻐해 주길 원해서, 이렇게 한 거야."
            T "음, 음... 그분을 향한 부정은, 눈 감아 줘..."

    $ change_image('smail_1')
    T "괜찮아. 어차피 어, 어른들은 우리가 아닌 다른 애를, 의심할 거야."
    T "그 애가 의식에서 죄인이 되면, 안 될 텐데..."
    T "그래도 우리는 괜찮으니까... [you]도 괜찮지?"

    menu:
        "잘못됐어.":
            $ belief += 3
            $ change_image('warry')
            T "뭐...? 자, 잘못됐다니. 아니 그건 맞, 맞지만..."
            $ change_image('what')
            T "나는 너를 위해서 그런, 거야...! 네, 네가 즐거웠으면 해서, 그래서..."
            T "뭐가 잘못됐단 거야? 허락되지 않은 음식을 먹어서? 너를 이 일에 끌어들여서? 아, 아니면 죄를 다른 사람에게 덮어씌운 게? 말하니까 많, 많네......"
            T "그치만 나는 호의로 한 행동이었어...! 이걸로 봐 주면 안 돼?"
            $ change_image('angry_1')
            T "읏... 싫, 싫어. 잘못했다고, 인정하고 싶지 않아... 다른, 다른 걸 할 거야."

        "네가 뭔데?":
            $ belief -= 2
            $ change_image('warry')
            T "어... 괘, 괜찮지 않은 거야?"
            T "그으... 그런 반응의 이유를, 모르겠어. 나 뭐 자, 잘못했어?"
            $ change_image('base_2')
            T "... 아... 내가 너, 너무 마음대로 행동했구나. 주제넘게..."
            T "미안, 앞으로는 자, 자제할게. 미안해."

        "잘했어.":
            $ belief -= 5
            $ change_image('smail_6')
            T "... ...!"
            T "응! 잘했, 잘했어!"
            $ change_image('smail_2')
            T "헤헤. 다, 다음에도 또 먹으, 러 오자."
            T "기분이 무척 좋아..."

    T "일단 나 우유, 돌려놓으러 가려는데... {은는}[you]{/은는} 어떡, 할래?"

    menu:
        "같이 가.":
            $ change_image('base_2')
            T "밖에 많이 추운데... 괜, 찮겠어?"
            T "음... 가, 가 보고 힘들면 다시 들어오자."
            
            hide hide_image
            scene black with dissolve

            "부엌의 문을 열면 또다시 세찬 찬바람이 실내로 침입하며 머리카락을 잔뜩 헤집었다."
            "코 끝이 시려워지는 걸 겨우 참으며 열 걸음 정도 떨어진 창고에 도착한 [lamb]가 우유통을 내려놓고 바닥에 널부러져 있는 상자를 뒤졌다."
            "짜잔! 열쇠를 획득했다! 당연히 잠금장치에 열쇠를 꽂으면 {i}달칵{/i}하는 경쾌한 소리와 함께 문이 열린다."
            
            scene eatBg2 with dissolve
            
            "들어온 내부에는 각종 식자재들이 쌓여있었다. 마침 겨울이니, 얼어도 되는 음식물을 보관해두는 걸까."
            "[lamb]가 익숙한 듯 빈 자리에 우유통을 올려두었다."

            menu:
                "멋대로 들어와도 돼?":
                    $ change_image('smail_1')
                    T "... 원래, 원래는 안 되겠지만... 드, 들어오는 애들은, 많아."
                    T "열쇠는 늘 같은 곳에 숨, 겨져 있고... 위치는 나도 ●●●한테 들었는걸. 모두들 아, 알고 있을 거야."
                    $ change_image('smail_2')
                    T "가끔 배고픈 애들이 몰래 들어왔다가 선생님한테 호, 혼난대. 히히."

                "간식 있어?":
                    $ change_image('base_3')
                    T "군것질을 할 만한 간식은 없어... 전에 ▲▲가 밤새 여기를 뒤, 졌는데도 애들용 먹을 것은, 없었대."
                    T "그 얘길 꺼, 꺼낸 뒤로 ▲▲는 안 보이지만... 잘 지내겠지?"

            # 좀... 안색 없는 걸로 바꿔...ㄹ까
            $ change_image('panic_6')
            T "에취...!" with vpunch
            T "자, 잠깐 있었는데 너무 추, 추워. 으으, 빨리 들어, 들어가자..."

            hide hide_image
            scene eatBg1 with dissolve

            "후다닥 밖으로 나와 문을 잠그고 열쇠를 돌려놓고 큰 걸음으로 다섯 번 다리를 움직여 따뜻한 실내로 돌아온다."
            $ change_image('cool')
            "꽤나 추웠던 건지 그 사이에 [lamb]는 피부를 파랗게 물들며 덜덜 떨고 있다."

        "다녀 와.":
            $ change_image('base_2')
            T "응 가, 갔다올게. 먼저 가 버리지 마, 알았지...?"
            "부엌의 문을 여니 다시 세찬 바람이 실내로 들이닥쳤다. 머리카락을 휘날리며 낑낑 통을 옮기는 뒷모습을 가만 본다."
            # 끼익...
            play sound "audio/sound/끼이익2.mp3"
            hide base_2 with dissolve
            "힘겹게 문이 닫혔으니... [lamb]의 말대로 기다릴까?"

            menu:
                "기다린다.":
                    $ belief += 3
                    "약속을 지키기 위해 얌전히 기다리자 얼마 시간이 지나지 않아 [lamb]가 돌아왔다."
                    # 못생겻다...
                    $ change_image('cool_2')
                    "피부에 빨간색과 파랑색이 섞여있는 모습이 이상하다."
                    T "... 저, 정말 기다려줬구나. 그렇, 그렇지. {은는}[you]{/은는} 상냥하니까, 날 버릴 리 없어..."
                    $ change_image('panic_2')
                    T "으... 아... 으엣취...!" with vpunch
                    T "쿨쩍... 미, 미안. 밖이 많이 추워서..."

                "버리고 간다.":
                    $ belief -= 2
                    "결정하고 자리에서 일어나 밖으로 가기 위해 뒤를 돌았을 때, {i}쾅!{/i} 큰 소리와 함께 문이 열리고 한기가 들어왔다."
                    "시선을 옮기면 거칠게 숨을 내쉬며 {을를}[you]{/을를} 응시하는 [lamb]가 보인다."
                    $ change_image('cool_2')
                    T "헉... 가, 가려던 거, 아니지? 그, 그치?"

                    menu:
                        "가려 했어.":
                            $ belief -= 3
                            $ change_image('sad_3')
                            T "왜, 왜...? 기다려 달라고 마, 말했잖아."
                            T "내가 그렇게... 귀찮아?"
                            T "거슬렸다면 미안, 미안해... 그래서 빨리 온," 
                            hide sad_3
                            $ change_image('cool')
                            extend " 엣취...!" with vpunch
                            T "흐악... 추, 추워..."

                        "잠깐 일어났어.":
                            $ belief += 1
                            $ change_image('extortion_1')
                            T "그, 그렇지? 내가 다 오해 한, 거야. 다행이다..." 
                            T "기다리게 해서, 미안해. 빨리 온다고 했..." 
                            hide extortion_1
                            $ change_image('cool')
                            extend " 으에취...!" with vpunch
                            T "흐읍... 미, 미안, 밖이 무척 추, 추워서..."

    menu:
        "무시한다.":
            $ belief -= 3
            $ change_image('cool_3')
            "나눌 수 있는 온기가 없었으므로 [lamb]는 스스로 어떻게든 추위를 이겨내려 애썼다."
            "여전히 추워보이긴 했지만... 시간도 걸렸지만..."
            $ change_image('hot')
            T "후우... ..."

        "체온을 나눈다.":
            $ belief += 3
            $ change_image('warry_2')
            "차가운 몸을 품에 안았다. 옷 너머 피부 안쪽까지 한기가 스며든다."
            "안긴 [lamb]는 당황했으나 널 밀어내진 않았고... 조심히 차가운 제 손을 뒤에 둔 채 슬쩍 [you]의 어깨에 이마를 기댔다."
            $ change_image('eye_bottom_2')
            "파랗게 물들었던 살갗에 점차 붉은색만이 남았다."
            "맞닿은 신체가 따끈따끈하다."
            $ change_image('hot')
            T "... 따뜻해서, 좋아... ..."

        "얼굴을 뎁힌다.":
            $ belief += 2
            $ change_image('warry_2')
            "[lamb]의 뺨을 양손으로 감싸면 손바닥 피부 너머로 차가움이 스며들었다. 살짝 눌린 얼굴에 당혹함이 가득한 건 무시하자."
            $ change_image('panic_3')
            "시선이 맞닿지 못하고 연신 빙글 돌아간다. 눈을 맞추기 힘들다."
            "그래도 얼굴은 금세 따뜻해져서, 아까의 차가움은 사라지고 미지근함만 남았다. 아니, [you]의 손이 같이 차가워진 건가?"
            $ change_image('smail_7')
            "[lamb]는 그저 웃었다."
            T "잘은 모르겠지만, 고마, 고마워."

    $ change_image('smail_1')
    T "나 때문에 감기에 걸리지 않, 았으면 좋겠는데... 나는 괜찮아 아마, 아마도. 아프면 무, 무리하지 마."
    T "... 보통은 이런 말을 좋아한대."
    T "이만 가, 갈까? 볼일은 다, 마쳤으니까..."
    T "오늘 {이랑}[you]{/이랑} 함께해서, 정말 좋았어. 만, 약에 다음이 있다면, 음..."
    $ change_image('smail_7')
    T "그때 또 같이 있자."
    T "잘 자. 내일 봐. 안녕."

    hide hide_image
    scene black
    with dissolve

    $ day += 1
    jump room 