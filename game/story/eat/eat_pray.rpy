label eat_pray:

    $ change_image('smail_5')
    T "좋아 그럼 기, 기도하자."
    $ change_image('pray_1')
    "두 손을 모으고 [lamb]가 눈을 감는다. 식전기도는 대부분 단순하고 개인적인 기도라지만..."

    menu:
        "축사를 해 줘.":
            $ belief += 3
            $ change_image('pray_4')
            T "어, 축사...? 그건 노, 높은 사람들이, 하는 거잖아. 내가 하면 그분이 시, 싫어하지 않으실까..."
            T "한 번도 해본 적, 없는데. 방법도 모, 몰라. 자리에서 일어, 나던가?"

            menu:
                "할 수 있어.":
                    $ belief += 3

                "안 할 거야?":
                    $ belief -= 2

                "그분이 기뻐하실 거야.":
                    $ belief += 5

            T "아, 알았어, 할게... 입으로 기도, 하는 거지... 내가 대표가 되어서..."
            $ change_image('pray_1')
            "큼큼. 긴장한 얼굴로 목을 가다듬고 다시 눈을 감는다. 이내 무언가 말하려 입술을 열었고—"

            if love:
                $ change_image('pray_2')
                "... 또다시 눈꺼풀이 열렸다. 눈치보듯 눈동자가 사방으로 굴러간다."
                T "... 그, 그런데 누구에게 기도해야, 돼? 내 신은 엘시인데, 엘시와 나의 만찬을 누구에게, 감사해야 되지? 요, 요리해 준 어른들?"

                menu:
                    "내게 해.":
                        $ belief += 3
                        T "그, 그렇구나. 바보 같은 고민을, 했네. 그냥 엘시에게 하면 되는 건데."
                        $ change_image('pray_5')
                        T "답을 주어서, 고마워 엘시."

                    "스스로 생각해.":
                        $ belief += 2
                        T "스스로는 너무 어, 어려워. 엘시가 알려주는 게, 제일 좋은데..."
                        T "아, 아냐. 직접 해야 엘시가 더 편하, 편하겠지...? 으응 생각할게... 생각해 틴, 아, 아그넬로. 생각 좀..."

                    "그냥 하지 마.":
                        $ belief += 5
                        $ change_image('pray_6')
                        T "... 아...! 아니, 아, 미안해. 이, 이런 것도 몰라서 미안해. 할, 테니까, 실망하지 말아 줘. 새, 생각할게. 할 수 있어. 난 할 수 있어...!"
                        T "조, 조금, 아주 조금만 기다... 잠깐, 만 제발..."
                        "불안정한 심호흡, 떨리는 숨소리가 몇 차례 울렸다."

                $ change_image('pray_1')
                T "... 스읍... 후..."
                "두 손을 가지런히 모으고 질끈 눈을 감는 [lamb]의 표정은 여전히 긴장으로 가득했지만, 이번에야말로 정말로 기도가 시작되었다."
                T "으음... 제일 먼저, 이 기도를 허락해 준 신에게 감사합니다."
                T "... 유일한 나의 신 엘시와 식사를 할 수 있음에 감사합니다."
                T "함께, 아, 아무런 문제 없이, 맛있는 요리를 먹을 수 있음에 감사합니다."
                T "우리의 일용할 양식을 위해 희, 생한 모든 것에 감사합니다."
                $ change_image('pray_4')
                T "으, 음. 끝, 끝났어. 이렇게 하는, 하는 게 맞을까?"

                menu:
                    "잘했어.":
                        $ belief += 4
                        $ change_image('smail_2')
                        T "저, 정말? 나 잘한, 거야? 다행, 다행이다."
                        T "앞으로도 더 노, 노력할게. 엘시에게 기쁨이 되고 싶어. 그러니까 계속... 함께 있어줘..."

                    "잘못됐어.":
                        $ belief -= 2
                        $ change_image('what')
                        T "잘못됐어? 어, 어떤 게? 난 잘 모르, 모르겠어. 엘시, 내가 뭘 잘못 한, 거야?"
                        T "느, 늘 부족해서 미안해. 고칠, 테니까... 바보 같은 어린 양을 용서해줘..."

                $ change_image('smail_1')
                T "엘시, 배고프진 않아? 내가 너무 붙잡, 았네. 이제 먹자. 어떤 마, 맛인지 기대 돼."

            else:
                "... 기도가 시작됐다."
                T "하아... 전능하고 자비로우신 분께. 저, 저희에게 일용할 양식을 허락, 해주셔서 감사합니다."
                T "어, 음... 모두 아, 아프지 말고, 건강하게 해주세요. 배탈이라도 나면 그, 그분의 뜻에 반하는, 거니까..."
                $ change_image('pray_2')
                T "끝... 이야. 뭔가 허, 허접한가...?"

                menu:
                    "잘했어.":
                        $ belief += 3
                        $ change_image('smail_2')
                        T "자, 잘했어? 그렇, 구나. 기쁘다..."
                        T "그분도 기뻐, 해주실까? {이가}[you]{/이가} 잘했다고 해, 했으니까 기뻐하시겠지?"
                        T "이런 기도는 처음이었는데... 더 연습해서 칭찬 받을래."
                        $ change_image('smail_5')

                    "별로야.":
                        $ belief -= 2
                        $ change_image('tension_1')
                        T "많이 별로였어? 아..."
                        T "어, 어쩔 수 없지. 처음이니까... 읏... 노력했는데..."
                        $ change_image('base_2')
                        T "이제 이런 건 안, 할래..."

                T "배, 배고프다. 이제 진짜로 먹자."

        "같이 기도한다.":
            $ belief += 3
            # 뭔가 어두워지는 연출을 쓰고 싶다
            show expression AlphaMask("black70", At(new, center)) as mask at multiply
            "[lamb]와 함께 [you]도 손을 모으고 눈을 감았다."
            "너는 어떻게 기도했어? 잘 먹겠습니다, 혹은 식사를 주셔서 감사합니다?"
            # 마스크를 숨겨말아
            $ change_image('smail_2', Fade(0.2, 0.2, 0.2))
            "알 방도는 없으나 짧은 침묵 끝에 마주한 [lamb]는 편안하게 웃고 있었다."
            "만약 왜 웃냐고 물어본다면, 그냥 너와 함께 기도해서 기뻤다고 말할 거야."

        "기도하지 않는다.":
            $ belief -= 3
            "어차피 눈을 감은 [lamb]는 {이가}[you]{/이가} 기도를 하는지 알 수가 없다."
            "숨소리만 들리는 짧은 고요가 지나갔다."
            $ change_image('smail_1')
            T "... 어... 계, 계속 날 보고 있었던, 거야?"
            T "아니, 아니야. 착각이었나 봐..."

